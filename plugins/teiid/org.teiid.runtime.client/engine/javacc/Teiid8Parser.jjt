options {
	STATIC = false;
	ERROR_REPORTING = true;
	JAVA_UNICODE_ESCAPE = true;
	UNICODE_INPUT = false;
	IGNORE_CASE = true;
    MULTI = true;
    BUILD_NODE_FILES = false;
    NODE_PREFIX = "";
    NODE_SCOPE_HOOK = false;
    NODE_USES_PARSER = true;
    NODE_PACKAGE = "org.teiid.query.sql.lang";
    VISITOR = false;
    VISITOR_RETURN_TYPE = "void";
    GRAMMAR_ENCODING="UTF-8";
    KEEP_LINE_COLUMN = true;
    NODE_FACTORY = "TeiidNodeFactory";
    NODE_DEFAULT_VOID = true;
}

PARSER_BEGIN(Teiid8Parser)

package org.teiid.query.parser.v8;

import java.math.BigInteger;
import java.util.*;
import java.util.regex.Matcher;
import org.teiid.designer.runtime.version.spi.ITeiidServerVersion;
import org.teiid.designer.runtime.version.spi.TeiidServerVersion;
import org.teiid.designer.runtime.version.spi.TeiidServerVersion.Version;
import org.teiid.designer.query.sql.lang.IMatchCriteria.MatchMode;
import org.teiid.designer.query.sql.lang.IJoinType;
import org.teiid.designer.query.sql.lang.ISetQuery;
import org.teiid.runtime.client.Messages;
import org.teiid.language.SortSpecification.NullOrdering;
import org.teiid.metadata.AbstractMetadataRecord;
import org.teiid.metadata.BaseColumn;
import org.teiid.metadata.BaseColumn.NullType;
import org.teiid.metadata.Column;
import org.teiid.metadata.KeyRecord;
import org.teiid.metadata.MetadataFactory;
import org.teiid.metadata.Procedure;
import org.teiid.metadata.ProcedureParameter;
import org.teiid.metadata.Table;
import org.teiid.query.parser.AbstractTeiidParser;
import org.teiid.query.parser.ParsedDataType;
import org.teiid.query.parser.ParseInfo;
import org.teiid.query.parser.TeiidNodeFactory;
import org.teiid.query.parser.TeiidNodeFactory.ASTNodes;
import org.teiid.query.sql.lang.*;
import org.teiid.query.sql.lang.CriteriaOperator.Operator;
import org.teiid.query.sql.lang.SubqueryCompareCriteria.PredicateQuantifier;
import org.teiid.query.sql.proc.*;
import org.teiid.query.sql.symbol.*;
import org.teiid.core.types.BinaryType;
import org.teiid.core.types.DataTypeManagerService;
import org.teiid.core.util.StringUtil;

/** 
 * <p>The SQLParser is a JavaCC-generated parser that reads a SQL string and produces a 
 * Query object.  The SQLParser.java file is generated by JavaCC from the SQLParser.jj
 * file.  <B>WARNING:</B> DO NOT MODIFY the SQLParser.java file as it will be 
 * regenerated from the .jj file and your changes will be lost!</p>
 */
@SuppressWarnings({"nls", "unused", "javadoc"})
public class Teiid8Parser extends AbstractTeiidParser {

    private String getComment(Token t) {
        Token optToken = t.specialToken;
        if (optToken == null) { 
            return ""; //$NON-NLS-1$
        }

        //handle nested comments
        String image = optToken.image;
        while (optToken.specialToken != null) {
            optToken = optToken.specialToken;
            image = optToken.image + image;
        }

        String hint = image.substring(2, image.length() - 2);
        if (hint.startsWith("+")) { //$NON-NLS-1$
            hint = hint.substring(1);
        }

        return hint;
    }

    private boolean isNonStrictHint(Token t) {
        String[] parts = getComment(t).split("\\s"); //$NON-NLS-1$
        for (int i = 0; i < parts.length; i++) {
            if (parts[i].equalsIgnoreCase(Limit.NON_STRICT)) {
                return true;
            }
        }
        return false;
    }

    private SourceHint getSourceHint(Token t) {
        String comment = getComment(t);
        Matcher matcher = SOURCE_HINT.matcher(comment);
        if (!matcher.find()) {
            return null;
        }

        SourceHint sourceHint = new SourceHint();
        if (matcher.group(1) != null) {
            sourceHint.setUseAliases(true);
        }

        String generalHint = matcher.group(2);
        if (generalHint != null) {
            sourceHint.setGeneralHint(normalizeStringLiteral(generalHint));
        }

        int end = matcher.end();
        matcher = SOURCE_HINT_ARG.matcher(comment);
        while (matcher.find(end)) {
            end = matcher.end();
            sourceHint.setSourceHint(matcher.group(1), normalizeStringLiteral(matcher.group(3)), matcher.group(2) != null);
        }

        return sourceHint;
    }

    private SourceHint getSourceHint(Teiid8Parser parser) {
       int index = 1;
       //scan for the first keyword
       Token t = null;
       do {
           t = parser.getToken(index++);
       } while (t != null && t.kind == LPAREN);
       t = parser.getToken(index);
       if (t == null) {
           return null;
       }

       return getSourceHint(t);
    }

    private SubqueryHint getSubqueryHint(Token t) {
        SubqueryHint hint = new SubqueryHint();
        String[] parts = getComment(t).split("\\s"); //$NON-NLS-1$
        for (int i = 0; i < parts.length; i++) {
            if (parts[i].equalsIgnoreCase(SubqueryHint.MJ)) {
                hint.setMergeJoin(true);
            } else if (parts[i].equalsIgnoreCase(SubqueryHint.NOUNNEST)) {
                hint.setNoUnnest(true);
            } else if (parts[i].equalsIgnoreCase(SubqueryHint.DJ)) {
                hint.setDepJoin();
            }
        }
        return hint;
    }

    private void setFromClauseOptions(Token groupID, FromClause fromClause){
        String[] parts = getComment(groupID).split("\\s"); //$NON-NLS-1$

        for (int i = 0; i < parts.length; i++) {
            if (parts[i].equalsIgnoreCase(Option.OPTIONAL)) {
                fromClause.setOptional(true);
            } else if (parts[i].equalsIgnoreCase(Option.MAKEDEP)) {
                fromClause.setMakeDep(true);
            } else if (parts[i].equalsIgnoreCase(Option.MAKENOTDEP)) {
                fromClause.setMakeNotDep(true);
            } else if (parts[i].equalsIgnoreCase(FromClause.MAKEIND)) {
                fromClause.setMakeInd(true);
            } else if (parts[i].equalsIgnoreCase(SubqueryHint.NOUNNEST)) {
                fromClause.setNoUnnest(true);
            } else if (parts[i].equalsIgnoreCase(FromClause.PRESERVE)) {
                fromClause.setPreserve(true);
            }
        }
    }

    @Override
    public ITeiidServerVersion getVersion() {
        if (version == null)
            version = new TeiidServerVersion("8.x.x");

        return version;
    }

    private void requires(boolean condition) throws ParseException {
      if (!condition)
        throw new ParseException(Messages.getString(
                Messages.Misc.TeiidVersionFailure, getVersion().toString()));
    }

} // end class

PARSER_END(Teiid8Parser)

TOKEN_MGR_DECLS : 
{
    int commentNestingDepth; 
} 

SKIP :
{
	" " | "\t" | "\n" | "\r"
}

MORE :
{
  "/*" { commentNestingDepth = 1 ; } : IN_MULTI_LINE_COMMENT
}

<IN_MULTI_LINE_COMMENT>
MORE : 
{  
  "/*" { commentNestingDepth += 1 ; } 
} 

<IN_MULTI_LINE_COMMENT>
SPECIAL_TOKEN : 
{  
  "*/" { 
    commentNestingDepth -= 1;
    SwitchTo( commentNestingDepth==0 ? DEFAULT : IN_MULTI_LINE_COMMENT ) ; 
  }
} 

<IN_MULTI_LINE_COMMENT>
MORE :
{
  < ~[] >
}

TOKEN: /* Data types */
{
	<STRING: "string">
|   <VARBINARY: "varbinary">
|   <VARCHAR: "varchar">
|	<BOOLEAN: "boolean">
|	<BYTE: "byte">
|   <TINYINT: "tinyint">
|	<SHORT: "short">
|   <SMALLINT: "smallint">
|	<CHAR: "char">
|	<INTEGER: "integer">
|	<LONG: "long">
|   <BIGINT: "bigint">
|	<BIGINTEGER: "biginteger">
|	<FLOAT: "float">
|   <REAL: "real"> 
|	<DOUBLE: "double">
|	<BIGDECIMAL: "bigdecimal">
|   <DECIMAL: "decimal">
|	<DATE: "date">
|	<TIME: "time">
|	<TIMESTAMP: "timestamp">
|	<OBJECT: "object">
|	<BLOB: "blob">
|	<CLOB: "clob">
|	<XML: "xml">
}


TOKEN: /* Functions with special syntax */
{
	<CAST: "cast">
|	<CONVERT: "convert">
}

TOKEN : /* Reserved words */
{
    <ADD: "add">
|   <ALL: "all">
|   <ALTER: "alter">
|   <AND: "and">
|   <ANY: "any">
|   <ARRAY: "array">
|   <ARRAY_AGG: "array_agg">
|   <AS: "as">
|   <ASC: "asc">
|   <ATOMIC: "atomic">
|   <AUTORIZATION: "authorization">
|   <BEGIN: "begin">
|   <BETWEEN: "between">
|   <BINARY: "binary">
|   <BOTH: "both">
|   <BREAK: "break">
|   <BY: "by">
|   <CALL: "call">
|   <CALLED: "called">
|   <CASCADED: "cascaded">
|   <CASE: "case">
|   <CHARACTER: "character">
|   <CHECK: "check">
|   <CLOSE: "close">
|   <COLLATE: "collate">
|   <COLUMN: "column">
|   <COMMIT: "commit">
|   <CONNECT: "connect">
|   <CONSTRAINT: "constraint">
|   <CONTINUE: "continue">
|   <CORRESPONDING: "corresponding">
|   <CURRENT_DATE: "current_date">
|   <CURRENT_TIME: "current_time">
|   <CURRENT_TIMESTAMP: "current_timestamp">
|   <CURRENT_USER: "current_user">
|   <CREATE: "create">
|   <CRITERIA: "criteria">
|   <CROSS: "cross">
|   <CURSOR: "cursor">
|   <DAY: "day">
|   <DEALLOCATE: "deallocate">
|   <DEFAULT_KEYWORD: "default">
|   <DECLARE: "declare">
|   <DELETE: "delete">
|   <DESC: "desc">
|   <DESCRIBE: "describe">
|   <DETERMINISTIC: "deterministic">
|   <DISCONNECT: "disconnect">
|   <DISTINCT: "distinct">
|   <DROP: "drop">
|	<EACH: "each">
|   <ELSE: "else">
|   <END: "end">
|   <ERROR: "error">
|   <ESCAPE: "escape">
|   <EXCEPT: "except">
|   <EXEC: "exec">
|   <EXECUTE: "execute">
|   <EXTERNAL: "external">
|   <EXISTS: "exists">
|   <FALSE: "false">
|   <FETCH: "fetch">
|   <FILTER: "filter">
|   <FOR: "for">
|   <FOREIGN: "foreign">
|   <FROM: "from">
|   <FULL: "full">
|   <FUNCTION: "function">
|   <GET: "get">
|   <GLOBAL: "global">
|   <GRANT: "grant">
|   <GROUP: "group">
|   <HAS: "has">
|   <HAVING: "having">
|   <HOLD: "hold">
|   <HOUR: "hour">
|   <IF: "if">
|   <IDENTITY: "identity">
|   <IMMEDIATE: "immediate">
|   <IN: "in">
|   <INDICATOR: "indicator">
|   <INNER: "inner">
|   <INPUT: "input">
|   <INOUT: "inout">
|   <INSENSITIVE: "insensitive">
|   <INSERT: "insert">
|   <INTERSECT: "intersect">
|   <INTERVAL: "interval">
|   <INTO: "into">
|   <IS: "is">
|   <ISOLATION: "isolation">
|   <JOIN: "join">
|   <LEFT: "left">
|   <LANGUAGE: "language">
|   <LARGE: "large">
|   <LEADING: "leading">
|   <LEAVE: "leave">
|   <LIKE: "like">
|   <LIKE_REGEX: "like_regex">
|   <LIMIT: "limit">
|   <LOCAL: "local">
|   <LOOP: "loop">
|   <MAKEDEP: "makedep">
|   <MAKENOTDEP: "makenotdep">
|   <MATCH: "match">
|   <MERGE: "merge">
|   <METHOD: "method">
|   <MINUTE: "minute">
|   <MODIFIES: "modifies">
|   <MODULE: "module">
|   <MONTH: "month">
|   <NATURAL: "natural">
|   <NEW: "new">
|	<NOCACHE: "nocache">
|   <NO: "no">
|   <NONE: "none">
|   <NOT: "not">
|   <NULL: "null">
|   <OF: "of">
|   <OFFSET: "offset">
|   <OLD: "old">
|   <ON: "on">
|   <ONLY: "only">
|   <OPEN: "open">
|   <OPTION: "option">
|   <OPTIONS: "options">
|   <OR: "or">
|   <ORDER: "order">
|   <OUT: "out">
|   <OUTER: "outer">
|   <OUTPUT: "output">
|   <OVER: "over">
|   <OVERLAPS: "OVERLAPS">
|   <PARAMETER: "parameter">
|   <PARTITION: "partition">
|   <PRECISION: "precision">
|   <PREPARE: "prepare">
|   <PRIMARY: "primary">
|   <PROCEDURE: "procedure">
|   <RANGE: "range">
|   <READS: "reads">
|   <RECURSIVE: "recursive">
|   <REFERENCES: "REFERENCES">
|   <REFERENCING: "REFERENCING">
|   <RETURN: "return">
|   <RETURNS: "returns">
|   <REVOKE: "REVOKE">
|   <RIGHT: "right">
|   <ROLLBACK: "ROLLBACK">
|   <ROLLUP: "ROLLUP">
|   <ROW: "row">
|   <ROWS: "rows">
|   <SAVEPOINT: "savepoint">
|   <SCROLL: "scroll">
|   <SEARCH: "search">
|   <SECOND: "second">
|   <SELECT: "select">
|   <SENSITIVE: "sensitive">
|   <SESSION_USER: "session_user">
|   <SET: "set">
|   <SIMILAR: "similar">
|   <SPECIFIC: "specific">
|   <SOME: "some">
|   <SQL: "sql">
|   <SQLEXCEPTION: "sqlexception">
|   <SQLSTATE: "sqlstate">
|   <SQLWARNING: "sqlwarning">
|   <START: "start">
|   <STATIC: "static">
|   <SYSTEM: "system">
|   <SYSTEM_USER: "system_user">
|   <TABLE: "table">
|   <TEMPORARY: "temporary">
|   <THEN: "then">
|   <TIMEZONE_HOUR: "timezone_hour">
|   <TIMEZONE_MINUTE: "timezone_minute">
|   <TO: "to">
|   <TRAILING: "trailing">
|   <TRANSLATE: "translate">
|   <TRIGGER: "trigger">
|   <TRUE: "true">
|   <UNION: "union">
|   <UNIQUE: "unique">
|   <UNKNOWN: "unknown">
|   <USER: "user">
|   <UPDATE: "update">
|   <USING: "using">
|   <VALUE: "value">
|   <VALUES: "values">
|   <VIRTUAL: "virtual">
|   <WHEN: "when">
|   <WHENEVER: "whenever">
|   <WHERE: "where">
|   <WITH: "with">
|   <WHILE: "while">
|   <WINDOW: "window">
|   <WITHIN: "within">
|   <WITHOUT: "without">
|   <YEAR: "year">

|   <ALLOCATE: "allocate">
|   <ARE: "are">
|   <ASENSITIVE: "asensitive">
|   <ASYMETRIC: "asymetric">
|   <CYCLE: "cycle">
|   <DEC: "dec">
|   <DEREF: "deref">
|   <DYNAMIC: "dynamic">
|   <ELEMENT: "element">
|   <FREE: "free">
|   <INT: "int">
|   <LATERAL: "lateral">
|   <LOCALTIME: "localtime">
|   <LOCALTIMESTAMP: "localtimestamp">
|   <MEMBER: "member">
|   <MULTISET: "multiset">
|   <NATIONAL: "national">
|   <NCHAR: "nchar">
|   <NCLOB: "nclob">
|   <NUMERIC: "numeric">
|   <RELEASE: "release">
|   <SPECIFICTYPE: "specifictype">
|   <SYMETRIC: "symetric">
|   <SUBMULTILIST: "submultilist">
|   <TRANSLATION: "translation">
|   <TREAT: "treat">
|   <VARYING: "varying">
}

TOKEN : /* SQL/XML Reserved words */
{
    <XMLAGG: "xmlagg">
|   <XMLATTRIBUTES: "xmlattributes">
|   <XMLBINARY: "xmlbinary">
|   <XMLCAST: "xmlcast">
|   <XMLCONCAT: "xmlconcat">
|   <XMLCOMMENT: "xmlcomment">
|   <XMLDOCUMENT: "xmldocument">
|   <XMLELEMENT: "xmlelement">
|   <XMLEXISTS: "xmlexists">
|   <XMLFOREST: "xmlforest">
|   <XMLITERATE: "xmliterate">
|   <XMLNAMESPACES: "xmlnamespaces">
|   <XMLPARSE: "xmlparse">
|   <XMLPI: "xmlpi">
|   <XMLQUERY: "xmlquery">
|   <XMLSERIALIZE: "xmlserialize">
|   <XMLTABLE: "xmltable">
|   <XMLTEXT: "xmltext">
|   <XMLVALIDATE: "xmlvalidate">
}

TOKEN : /* SQL/MED Reserved words */
{
    <DATALINK: "datalink">
|   <DLNEWCOPY: "dlnewcopy">
|   <DLPREVIOUSCOPY: "dlpreviouscopy">
|   <DLURLCOMPLETE: "dlurlcomplete">
|   <DLURLCOMPLETEWRITE: "dlurlcompletewrite">
|   <DLURLCOMPLETEONLY: "dlurlcompleteonly">
|   <DLURLPATH: "dlurlpath">
|   <DLURLPATHWRITE: "dlurlpathwrite">
|   <DLURLPATHONLY: "dlurlpathonly">
|   <DLURLSCHEME: "dlurlscheme">
|   <DLURLSERVER: "dlurlserver">
|   <DLVALUE: "dlvalue">
|   <IMPORT: "import">
}

TOKEN : /* NonReserved words */
{
    <INSTEAD: "instead">
|   <ENABLED: "enabled">
|   <DISABLED: "disabled">
|   <VIEW: "view">
|   <KEY: "key">
|   <SERIAL: "serial">
|   <TEXTAGG: "textagg">
|   <COUNT: "count">
|   <ROW_NUMBER: "row_number">
|   <RANK: "rank">
|   <DENSE_RANK: "dense_rank">
|   <SUM: "sum">
|   <AVG: "avg">
|   <MIN: "min">
|   <MAX: "max">
|   <EVERY: "every">
|   <STDDEV_POP: "stddev_pop">
|   <STDDEV_SAMP: "stddev_samp">
|   <VAR_SAMP: "var_samp">
|   <VAR_POP: "var_pop">
|   <DOCUMENT: "document">
|   <CONTENT: "content">
|   <TRIM: "trim">
|   <EMPTY: "empty">
|   <ORDINALITY: "ordinality">
|   <PATH: "path">
|   <FIRST: "first">
|   <LAST: "last">
|   <NEXT: "next">
|   <SUBSTRING: "substring">
|   <EXTRACT: "extract">
|   <TO_CHARS: "to_chars">
|   <TO_BYTES: "to_bytes">
|   <TIMESTAMPADD: "timestampadd">
|   <TIMESTAMPDIFF: "timestampdiff">
|   <QUERYSTRING: "querystring">
|   <NAMESPACE: "namespace">
|   <RESULT: "result">
|   <INDEX: "index">
|   <ACCESSPATTERN: "accesspattern">
|   <AUTO_INCREMENT: "auto_increment">
|   <WELLFORMED: "wellformed">
|   <SQL_TSI_FRAC_SECOND: "sql_tsi_frac_second">
|   <SQL_TSI_SECOND: "sql_tsi_second">
|   <SQL_TSI_MINUTE: "sql_tsi_minute">
|   <SQL_TSI_HOUR: "sql_tsi_hour">
|   <SQL_TSI_DAY: "sql_tsi_day">
|   <SQL_TSI_WEEK: "sql_tsi_week">
|   <SQL_TSI_MONTH: "sql_tsi_month">
|   <SQL_TSI_QUARTER: "sql_tsi_quarter">
|   <SQL_TSI_YEAR: "sql_tsi_year">
|   <TEXTTABLE: "texttable">
|   <ARRAYTABLE: "arraytable">
|   <SELECTOR: "selector">
|   <SKIP_KEYWORD: "skip">
|   <WIDTH: "width">
|   <PASSING: "passing">
|   <NAME: "name">
|   <ENCODING: "encoding">
|   <COLUMNS: "columns">
|   <DELIMITER: "delimiter">
|   <QUOTE: "quote">
|   <HEADER: "header">
|   <NULLS: "nulls">
|   <OBJECTTABLE: "objecttable">
|   <VERSION: "version">
|   <INCLUDING: "including">
|   <EXCLUDING: "excluding">
|   <XMLDECLARATION: "xmldeclaration">
|   <VARIADIC: "variadic">
|   <EXCEPTION: "exception">
|   <RAISE: "raise">
|   <CHAIN: "chain">
|   <JSONARRAY_AGG: "jsonarray_agg">
|   <JSONOBJECT: "jsonobject">

}
/*
name=all in group identifier
*/
TOKEN : { < ALL_IN_GROUP: <ID> <PERIOD> <STAR> > }
/*
name=identifier
*/
TOKEN : { < ID: <QUOTED_ID> (<PERIOD> <QUOTED_ID>)* > }
TOKEN : { < #QUOTED_ID: <ID_PART> | ("\"" (("\"\"") | ~["\""] )+ "\"") >  }     
TOKEN : { < #ID_PART: (("@" | "#" | <LETTER>) (<LETTER> | "_" | <DIGIT>)*) > }
/*
name=escaped function
*/
TOKEN : { < ESCAPEDFUNCTION: "{" "fn" > }
/*
name=escaped join
*/
TOKEN : { < ESCAPEDJOIN: "{" "oj" > }
/*
name=escaped type
*/
TOKEN : { < ESCAPEDTYPE: "{" ("d" | "t" | "ts" | "b") > }
/*
name=decimal numeric literal
*/
TOKEN : { < DECIMALVAL: (<DIGIT>)* <PERIOD> <UNSIGNEDINTEGER> > }
/*
name=approximate numeric literal
*/
TOKEN : { < FLOATVAL: <DIGIT> <PERIOD> <UNSIGNEDINTEGER> 
				["e", "E"] (<PLUS>|<MINUS>)? <UNSIGNEDINTEGER> > }
/*
name=string literal
*/				
TOKEN : { < STRINGVAL: (("N"|"E")? "'" ( ("''") | ~["'"] )* "'") > }
TOKEN : { < #LETTER: (["a"-"z","A"-"Z"] | ["\u0153"-"\ufffd"]) > }
TOKEN : { < #DIGIT: ["0"-"9"] > }
/*
name=unsigned integer literal
*/
TOKEN : { < UNSIGNEDINTEGER: (<DIGIT>)+ > }
/*
name=binary string literal
*/
TOKEN : { < BINARYSTRINGVAL: ("X"|"x" "'" ( <HEXIT><HEXIT> )+ "'") > }
TOKEN : { < #HEXIT: (["a"-"f","A"-"F"] | <DIGIT> ) > }

TOKEN :
{
	<COMMA: ",">
|	<PERIOD: ".">
|	<LPAREN: "(">
|	<RPAREN: ")">
|   <LBRACE: "{">
|   <RBRACE: "}">
|   <LSBRACE: "[">
|   <RSBRACE: "]">
|	<EQ: "=">
|	<NE: "<>">
|	<NE2: "!=">
|	<LT: "<">
|	<LE: "<=">
|	<GT: ">">
|	<GE: ">=">
|	<STAR: "*">
|	<SLASH: "/">
|	<PLUS: "+">
|	<MINUS: "-">
|   <QMARK: "?">
|   <DOLLAR: "$">
|   <SEMICOLON: ";">
|   <COLON: ":">
|	<CONCAT_OP: "||">
}

/*
name=string
description=A string literal value.  Use '' to escape ' in the string.  
example={code:sql}'a string'{code}\n{code:sql}'it''s a string'{code}
*/
String stringVal() :
{
	Token t = null;	
}
{
  	t = <STRINGVAL>
  	{
  		return normalizeStringLiteral(t.image);
  	}
}

/*
name=non-reserved identifier
description=Allows non-reserved keywords to be parsed as identifiers  
example=SELECT *COUNT* FROM ...
*/
Token nonReserved() :
{
}
{
    (<INSTEAD>|<VIEW>|<ENABLED>|<DISABLED>|<KEY>|<SERIAL>|<TEXTAGG>|<COUNT>|<ROW_NUMBER>|<RANK>|<DENSE_RANK>|<SUM>|<AVG>|<MIN>|<MAX>|<EVERY>|<STDDEV_POP>
     |<STDDEV_SAMP>|<VAR_SAMP>|<VAR_POP>|<DOCUMENT>|<CONTENT>|<TRIM>|<EMPTY>|<ORDINALITY>|<PATH>|<FIRST>|<LAST>|<NEXT>|<SUBSTRING>|<EXTRACT>|<TO_CHARS>
     |<TO_BYTES>|<TIMESTAMPADD>|<TIMESTAMPDIFF>|<QUERYSTRING>|<NAMESPACE>|<RESULT>|<INDEX>|<ACCESSPATTERN>|<AUTO_INCREMENT>|<WELLFORMED>|<SQL_TSI_FRAC_SECOND>
     |<SQL_TSI_SECOND>|<SQL_TSI_MINUTE>|<SQL_TSI_HOUR>|<SQL_TSI_DAY>|<SQL_TSI_WEEK>|<SQL_TSI_MONTH>|<SQL_TSI_QUARTER>|<SQL_TSI_YEAR>|<TEXTTABLE>|<ARRAYTABLE>
     |<SELECTOR>|<SKIP_KEYWORD>|<WIDTH>|<PASSING>|<NAME>|<ENCODING>|<COLUMNS>|<DELIMITER>|<QUOTE>|<HEADER>|<NULLS>|<OBJECTTABLE>
     |<VERSION>|<INCLUDING>|<EXCLUDING>|<XMLDECLARATION>|<VARIADIC>|<RAISE>|<EXCEPTION>|<CHAIN>|<JSONARRAY_AGG>|<JSONOBJECT>)
    {
    	return getToken(0);
    }	
}

/*
name=identifier
description=Partial or full name of a single entity.
example={code:sql}tbl.col{code}\n{code:sql}"tbl"."col"{code}
*/
String id(Boolean validate) :
{
}
{
  	(<ID>|nonReserved())
  	{
  		String id = normalizeId(getToken(0).image);
  		if (validate != null) {
  		    try {
  			   return validateName(id, validate);
  			 } catch (Exception ex) {
  			   throw new ParseException(ex.getLocalizedMessage());
  		     }
  		}
  		return id;
  	}
}

/* TODO: createProcedure should not be here, but is due to all of the legacy tests
unused=true
*/
Command command(ParseInfo info) :
{
    Command command = null;
}
{
    (LOOKAHEAD(2) command = createProcedure(info) |
     command = userCommand(info) |
     command = callableStatement(info)
	)
	[<SEMICOLON>]
	<EOF>
	{
		return command;
	}
}

/*
unused=true
*/
Command designerCommand(ParseInfo info) :
{
	Command command = null;
}
{
	(LOOKAHEAD (2) command = createProcedure(info) |
	 command = forEachRowTriggerAction(info) |
	 command = userCommand(info)	  
	)
	[<SEMICOLON>]
	<EOF>
	{
		return command;
	}
}

/*
name=create trigger
description=Creates a trigger action on the given target.
example={code:sql}CREATE TRIGGER ON vw INSTEAD OF INSERT AS FOR EACH ROW BEGIN ATOMIC ... END{code}
*/
Command createTrigger(ParseInfo info) :
{
  	String target = null;
  	TriggerAction triggerAction = null;
  	Token event = null;
}
{
	<CREATE> <TRIGGER> <ON> target = id(null) <INSTEAD> <OF> 
	(event = <INSERT> | event = <UPDATE> | event = <DELETE>) 
	<AS> triggerAction = forEachRowTriggerAction(info)
	{
		AlterTrigger alterTrigger = alterTrigger();
		alterTrigger.setTarget(groupSymbol(target));
		alterTrigger.setDefinition(triggerAction);
		alterTrigger.setEvent(Table.TriggerEvent.valueOf(event.image.toUpperCase()));
		alterTrigger.setCreate(true);
		return alterTrigger;
	}
}

/*
name=alter
description=Alter the given target.
example={code:sql}ALTER VIEW vw AS SELECT col FROM tbl{code}
*/
Command alter(ParseInfo info) :
{
  	String target = null;
  	QueryCommand command = null;
  	Statement stmt = null;
  	TriggerAction triggerAction = null;
  	Token comment = null;
  	Token event = null;
  	Boolean enabled = null;
}
{
	<ALTER>
	(
	 	(<VIEW> target = id(null) <AS> { comment = getToken(1).specialToken; } command = queryExpression(info)) 
	 	{
	 	    /*
	 	     * if (comment != null) {
		 	 *     command.setCacheHint(getQueryCacheOption(comment.image));
	 	     * }
	 	     */
            AlterView alterView = alterView();
	 		alterView.setTarget(groupSymbol(target));
	 		alterView.setDefinition(command);
	 		return alterView;
	 	}
		| (<PROCEDURE> target = id(null) <AS> { comment = getToken(1).specialToken; } stmt = statement(info)) 
		{
			CreateProcedureCommand cup = createASTNode(ASTNodes.CREATE_PROCEDURE_COMMAND);
			cup.setBlock(asBlock(stmt));
			/*
			 * if (comment != null) {
		 	 *     cup.setCacheHint(getQueryCacheOption(comment.image));
	 	     * }
	 	     */
            AlterProcedure alterProc = alterProcedure();
			alterProc.setTarget(groupSymbol(target));
			alterProc.setDefinition(cup);
			return alterProc;
		}
		| (<TRIGGER> <ON> target = id(null) <INSTEAD> <OF> 
		  (event = <INSERT> | event = <UPDATE> | event = <DELETE>) 
		  ((<AS> triggerAction = forEachRowTriggerAction(info))
		  |<ENABLED> {enabled=true;}| <DISABLED> {enabled=false;}))
		{
		    AlterTrigger alterTrigger = alterTrigger();
			alterTrigger.setTarget(groupSymbol(target));
			alterTrigger.setDefinition(triggerAction);
			alterTrigger.setEvent(Table.TriggerEvent.valueOf(event.image.toUpperCase()));
			if (enabled != null) {
				alterTrigger.setEnabled(enabled);
			}
			return alterTrigger;
		}
	)
}

/*
name=for each row trigger action
description=Defines an action to perform on each row.
example={code:sql}FOR EACH ROW BEGIN ATOMIC ... END{code}
*/
TriggerAction forEachRowTriggerAction(ParseInfo info) #TriggerAction :
{
	Block b = block();
	b.setAtomic(true);
	Statement stmt = null;
}
{
	<FOR> <EACH> <ROW>
	(
	 LOOKAHEAD(1) (<BEGIN> [<ATOMIC>]
      (
	    stmt = statement(info)
	    {          
    	    b.addStatement(stmt);            	                	    
	    }
      )*
      <END> 
     )
     |
     stmt = statement(info)
     {
       b = asBlock(stmt);
       b.setAtomic(true);
     }
    ) 
	{
		jjtThis.setBlock(b);
		return jjtThis;
	}
}

/*
name=directly executable statement
description=A statement that can be executed at runtime.
example={code:sql}SELECT * FROM tbl{code}
index=true
*/
Command userCommand(ParseInfo info) :
{
	Command command = null;
	SourceHint sourceHint = null;
}
{
    {
      if (versionLessThan(Version.TEIID_8_4)) {
        int index = 1;
        Token t = null;
        do
          t = getToken(index++);
        while (t != null && t.kind == LPAREN);
        t = getToken(index);
        if (t != null)
          sourceHint = getSourceHint(t);
      }
    }
	(command = queryExpression(info) |
	command = storedProcedure(info, 1) |
    { if (versionAtLeast(Version.TEIID_8_4)) sourceHint = getSourceHint(this); } command = insert(info) |
    { if (versionAtLeast(Version.TEIID_8_4)) sourceHint = getSourceHint(this); } command = update(info) |
    { if (versionAtLeast(Version.TEIID_8_4)) sourceHint = getSourceHint(this); } command = delete(info) |
    command = dropTable(info) |
	LOOKAHEAD(2) command = createTempTable(info) |
    LOOKAHEAD(2) command = createForeignTempTable(info) |
	command = alter(info) |
	command = createTrigger(info)
	)
	{
	    setSourceHint(sourceHint, command);
		return command;
	}
}

/*
name=drop table
description=Creates a trigger action on the given target.
example={code:sql}CREATE TRIGGER ON vw INSTEAD OF INSERT AS FOR EACH ROW BEGIN ATOMIC ... END{code}
*/
Drop dropTable(ParseInfo info) #Drop :
{
	String table = null;
}
{
	<DROP> <TABLE> 
	table = id(null)
	{
		jjtThis.setTable(groupSymbol(table));
		return jjtThis;
	}
}

/*
name=create temporary table
description=Creates a temporary table.
example={code:sql}CREATE LOCAL TEMPORARY TABLE tmp (col integer){code}
*/
Command createTempTable(ParseInfo info) #Create :
{
    String table = null;
    String pkId = null;
    Column col = null;
    List<ElementSymbol> pk = null;
}
{
    <CREATE> <LOCAL> <TEMPORARY> <TABLE> 
    table = id(null)
    <LPAREN>
    {
        jjtThis.setTable(groupSymbol(table));
    }
    col = tableElement(info)
    {
      jjtThis.getColumns().add(col);
    }
    (LOOKAHEAD(2) <COMMA>
      col = tableElement(info)
      {
        jjtThis.getColumns().add(col);
      }
    )*  
    [<COMMA> <PRIMARY> <KEY> 
      pk = columnList(true, true)
      {
        jjtThis.getPrimaryKey().addAll(pk);
      }   
    ]
    <RPAREN>
    {
        return jjtThis;
    }
}

/*
name=temporary table element
description=Defines a temporary table column.
example={code:sql}col string NOT NULL{code}
*/
Column tableElement(ParseInfo info) :
{
    String element = null;
    ParsedDataType type = null;
    boolean autoIncrement = false;
    //TODO: once we support udts, then this will need to inherit the nullability from the type
    boolean notNull = false;
}
{
     element = id(Boolean.TRUE)
     (
      type = parseDataType()
      |
      <SERIAL>
      {
        type = new ParsedDataType("INTEGER");
        autoIncrement = true;
        notNull = true;
      }
     )
     [<NOT> <NULL> { notNull = true; }]
     {
        Column c = new Column(getVersion());
        c.setName(element);
        c.setRuntimeType(type.getType());
        c.setAutoIncremented(autoIncrement);
        c.setNullType(notNull?NullType.No_Nulls:NullType.Nullable);
        return c;
     }
}

/*
name=raise error statement
description=Raises an error with the given message.
example={code:sql}ERROR 'something went wrong'{code}
*/
RaiseStatement errorStatement(ParseInfo info) :
{
    Expression errMsg = null;
}
{ 
    <ERROR>
    errMsg = expression(info)

    {
        ExceptionExpression ee = exceptionExpression();
        ee.setMessage(errMsg);
        return raiseStatementFromExpression(ee);
    }
}

/*
name=raise statement
description=Raises an error or warning with the given message.
example={code:sql}RAISE SQLEXCEPTION 'something went wrong'{code}
*/
RaiseStatement raiseStatement(ParseInfo info) :
{
    RaiseStatement stmt = null;
    Expression err = null;
    boolean warning = false;
}
{ 
    <RAISE>
    [<SQLWARNING> {warning = true;}]
    err = exceptionReference(info)
    {
        stmt = raiseStatementFromExpression(err);
        stmt.setWarning(warning);
        return stmt;       
    }
}

RaiseStatement raiseStatementFromExpression(Expression expression) #RaiseStatement :
{
}
{
    {
      jjtThis.setExpression(expression);
      return jjtThis;
    }
}

/*
name=exception reference
description=a reference to an exception
example={code:sql}SQLEXCEPTION 'something went wrong' SQLSTATE '00X', 2{code}
*/
Expression exceptionReference(ParseInfo info) :
{
	String id = null;
	Expression ex;
}
{
	id = id(null) {return elementSymbol(id);}
	|
	ex = exception(info)
	{
		return ex;
	}
}

/*
name=sql exception
description=creates a sql exception or warning with the specified message, state, and code
example={code:sql}SQLEXCEPTION 'something went wrong' SQLSTATE '00X', 2{code}
*/
Expression exception(ParseInfo info) :
{
	Expression err = null;
	Expression sqlState = null;
	Expression errCode = null;
	Expression parent = null;
}
{
    <SQLEXCEPTION>
	err = commonValueExpression(info)
	[<SQLSTATE> sqlState = commonValueExpression(info)
     [<COMMA> errCode = commonValueExpression(info)]
    ]
    [<CHAIN> parent = exceptionReference(info)]
	{
		ExceptionExpression ee = exceptionExpression();
		ee.setMessage(err);
		ee.setSqlState(sqlState);
		ee.setErrorCode(errCode);
		ee.setParent(parent);
		return ee;
	}	
}

ExceptionExpression exceptionExpression() #ExceptionExpression :
{
}
{
    {
        return jjtThis;
    }
}

/*
name=statement
description=A procedure statement.
example={code:sql}IF (x = 5) BEGIN ... END{code}
*/
Statement statement(ParseInfo info) :
{
    Statement stmt;
    String label = null;
}
{
    (
	 LOOKAHEAD(2) ([label = id(null) <COLON>] 
	  (
	   stmt = loopStatement(info) |
	   stmt = whileStatement(info) |
	   stmt = compoundStatement(info)
	  )
	  {
	  	((Labeled)stmt).setLabel(label);
	  }
	 )
	 |
	 (
      stmt = ifStatement(info) |
      stmt = delimitedStatement(info)
	 )
	)
    
    {
        return stmt;
    }
}

/*
name=delimited statement
description=A procedure statement terminated by ;.
example={code:sql}SELECT * FROM tbl;{code}
*/
Statement delimitedStatement(ParseInfo info) :
{
    Statement stmt = null;
}           
{
     (LOOKAHEAD(2) stmt = assignStatement(info) |
	  stmt = sqlStatement(info) |
      stmt = errorStatement(info) |
      stmt = raiseStatement(info) |
	  stmt = declareStatement(info) |
	  stmt = branchingStatement(info) |
	  stmt = returnStatement(info)
	 )
	 <SEMICOLON>
	 {
	     return stmt;
	 }
}

/*
name=compound statement
description=A procedure statement block contained in BEGIN END.
example={code:sql}BEGIN NOT ATOMIC ... END{code}
*/
Block compoundStatement(ParseInfo info) :
{
    Statement stmt = null;
    Block block = block();
    Boolean atomic = null;
    String eId = null;
}
{
    <BEGIN> [[<NOT> {atomic = Boolean.FALSE;}] <ATOMIC> {if (atomic == null) {atomic = Boolean.TRUE;}}]
    (LOOKAHEAD(2)
      stmt = statement(info)
      {
        block.addStatement(stmt, false);            	                	    
	  }
 	)*
 	[<EXCEPTION> eId = id(Boolean.FALSE) {block.setExceptionGroup(eId);}
 	 (
       stmt = statement(info)
       {
         block.addStatement(stmt, true);            	                	    
	   }
 	 )*
 	]
    <END>
    {
        if (atomic != null) {
        	block.setAtomic(atomic);
        }
        return block;
    }
}        

/*
name=branching statement
description=A procedure branching control statement, which typically specifies a label to return control to.
example={code:sql}BREAK x{code}
*/
BranchingStatement branchingStatement(ParseInfo info) #BranchingStatement :
{
    Token mode = null;
    String label = null;
}
{
 	(
 	 (
 	  (mode = <BREAK> | mode = <CONTINUE>) [label = id(null)] 
 	 )
 	 | 
 	 (mode = <LEAVE> label = id(null))
 	)
 	{
 	    jjtThis.setMode(BranchingStatement.BranchingMode.valueOf(mode.image.toUpperCase()));
 	    jjtThis.setLabel(label);
 		return jjtThis;
 	}
} 	

/*
name=return statement
description=A return statement.
example={code:sql}RETURN 1{code}
*/
ReturnStatement returnStatement(ParseInfo info) #ReturnStatement :
{
    Expression ex = null;
}
{
 	<RETURN> [ex = expression(info)]
 	{
 	    jjtThis.setExpression(ex);
 	    return jjtThis;
 	}
} 	

/*
name=while statement
description=A procedure while statement that executes until its condition is false.
example={code:sql}WHILE (var) BEGIN ... END{code}
*/
WhileStatement whileStatement(ParseInfo info) #WhileStatement :
{
    Criteria criteria = null;
    Statement stmt = null;
}
{
    <WHILE>
    <LPAREN>  
    criteria = criteria(info)
    <RPAREN>
    stmt = statement(info)
    {
        jjtThis.setCondition(criteria);
        jjtThis.setBlock(asBlock(stmt));
        return jjtThis;
    }    
}    

/*
name=loop statement
description=A procedure loop statement that executes over the given cursor.
example={code:sql}LOOP ON (SELECT * FROM tbl) AS x BEGIN ... END{code}
*/
LoopStatement loopStatement(ParseInfo info) #LoopStatement :
{
    String cursor = null;
    QueryCommand query = null;
    Statement stmt = null;
}
{
    <LOOP>
    <ON>
    <LPAREN>  
    query = queryExpression(info)
    <RPAREN>
    <AS>
    cursor = id(null)
    stmt = statement(info)
    {
        jjtThis.setBlock(asBlock(stmt));
        jjtThis.setCommand(query);
        jjtThis.setCursorName(cursor);   
        return jjtThis;
    }    
}   

/*
name=if statement
description=A procedure loop statement that executes over the given cursor.
example={code:sql}IF (boolVal) BEGIN variables.x = 1 END ELSE BEGIN variables.x = 2 END{code}
*/
IfStatement ifStatement(ParseInfo info) #IfStatement :
{
    Criteria criteria = null;
    Statement ifStatement = null;
    Statement elseStatement = null;
}
{
    <IF>
    <LPAREN>  
    criteria = criteria(info)
    <RPAREN>
    ifStatement = statement(info)
    //else blocks will be associated with the closest if block
    [LOOKAHEAD(1)<ELSE> elseStatement = statement(info)]
    {
        jjtThis.setCondition(criteria);
        jjtThis.setIfBlock(asBlock(ifStatement));
        jjtThis.setElseBlock(asBlock(elseStatement));
        return jjtThis;
    }    
}    
            
/*
name=declare statement
description=A procedure declaration statement that creates a variable and optionally assigns a value.
example={code:sql}DECLARE STRING x = 'a'{code}
*/
DeclareStatement declareStatement(ParseInfo info) #DeclareStatement :
{
    String var = null;    
    ParsedDataType type = null;  
    ElementSymbol variableID = null;
    LanguageObject value = null;
}
{
    <DECLARE>
    (type = parseDataType() | <EXCEPTION> {type = new ParsedDataType(getToken(0).image);})
    var = id(null)
    {
        variableID = elementSymbol(var);
    }
    [<EQ>
     value = assignStatementOperand(info)
    ]
    
    {
        jjtThis.setVariable(variableID);
        jjtThis.setVariableType(type.getType());
        if (value instanceof Expression) {
            jjtThis.setExpression((Expression)value);
    	}
    	else if (value instanceof QueryCommand) {
    	    jjtThis.setExpression(scalarSubquery((QueryCommand)value));
    	}
    	else {
    	    jjtThis.setCommand((Command)value);
    	}

        return jjtThis;
    }
}

/*
name=assignment statement
description=Assigns a variable a value in a procedure.
example={code:sql}x = 'b'{code}
*/
Statement assignStatement(ParseInfo info) :
{
    LanguageObject value = null;
    String var = null;
    ElementSymbol elementID = null;
    boolean returnable=true;
}
{
    var = id(null)
    {
        elementID = elementSymbol(var);              
    } 
    <EQ>
    (value = assignStatementOperand(info) |
     (value = storedProcedure(info, 2) [(<WITH>|<WITHOUT> {returnable = false;}) <RETURN>]) {
		StoredProcedure storedProcedure = (StoredProcedure)value;
	    SPParameter parameter = new SPParameter(this, 1, SPParameter.RETURN_VALUE, "return"); //$NON-NLS-1$
	    parameter.setExpression(elementID);
		storedProcedure.setParameter(parameter);  
		storedProcedure.setCalledWithReturn(true);
		CommandStatement cs = createASTNode(ASTNodes.COMMAND_STATEMENT);
		cs.setCommand(storedProcedure);
		cs.setReturnable(returnable);
		return cs;      	    
     }
    )

    {
        return assignmentStatement(elementID, value);
    }    
}

/*
name=assignment statement operand
description=A value or command that can be used in an assignment.  {note}All assigments except for expression are deprecated.{note}
*/
LanguageObject assignStatementOperand(ParseInfo info) :
{
    LanguageObject value = null;
}
{
    (
	  LOOKAHEAD(<INSERT><INTO>) (value = insert(info)) | //deprecated
      value = update(info) | //deprecated
      value = delete(info) | //deprecated
      LOOKAHEAD(1) value = expression(info) |      
      value = queryExpression(info) | //deprecated should be a scalar subquery
      value = exception(info)
    ) 
    {   
        return value;
    }    
}

/*
name=data statement
description=A procedure statement that executes a SQL statement.  An update statement can have its update count accessed via the ROWCOUNT variable. 
*/
CommandStatement sqlStatement(ParseInfo info) #CommandStatement :
{
    Command cmd = null;
    String var = null;
    ElementSymbol elementID = null;
    StoredProcedure storedProcedure = null;
    boolean returnable = true;
}
{
    (LOOKAHEAD(2) cmd = userCommand(info) |
     cmd = dynamicCommand(info)
    ) [(<WITH>|<WITHOUT> {returnable = false;}) <RETURN>]
  
    {
        jjtThis.setCommand(cmd);
    	jjtThis.setReturnable(returnable);
    	return jjtThis;
    }   
}
 
/*
unused=true
*/
CreateProcedureCommand createProcedure(ParseInfo info) # CreateProcedureCommand :
{
     Statement stmt = null;
}
{ 
    <CREATE> [<VIRTUAL>] <PROCEDURE>
    stmt = statement(info)
    {
        jjtThis.setBlock(asBlock(stmt));
        return jjtThis;
    }
}

/*
name=procedure body definition
description=Defines a procedure body on a Procedure metadata object.
example={code:sql}CREATE VIRTUAL PROCEDURE BEGIN ... END{code}
index=true
*/
CreateProcedureCommand procedureBodyCommand(ParseInfo info) :
{
    CreateProcedureCommand procCmd = createASTNode(ASTNodes.CREATE_PROCEDURE_COMMAND);
    Statement stmt = null;
}
{ 
    [LOOKAHEAD(2) <CREATE> [<VIRTUAL>] <PROCEDURE>]
    stmt = statement(info)
    {
        procCmd.setBlock(asBlock(stmt));
        return procCmd;
    }
} 

/*
name=dynamic data statement
description=A procedure statement that can execute arbitrary sql.
example={code:sql}EXECUTE IMMEDIATE 'SELECT * FROM tbl' AS x STRING INTO #temp{code}
*/
DynamicCommand dynamicCommand(ParseInfo info) #DynamicCommand :
{
    Expression sql = null;           
    String groupID = null;
	GroupSymbol group = null;
	int updateCount = 0;
	List<ProjectedColumn> elements = null;
	SetClauseList using = null;
	SetClauseList setClauseList = null;
}
{ 
    (<EXECUTE>|<EXEC>) [(<STRING>|<IMMEDIATE>)]
    sql = expression(info)
    {
        jjtThis.setSql(sql);
    }
    [<AS> 
	elements = createElementsWithTypes(info)

	 [<INTO>
      groupID = id(null)
      {
         group = groupSymbol(groupID);
      }
     ]
     {
         jjtThis.setIntoGroup(group);
         List<ElementSymbol> symbols = new ArrayList(elements.size());
         for (ProjectedColumn col : elements) {
         	symbols.add(col.getSymbol());
         }
         jjtThis.setAsColumns(symbols);
         jjtThis.setAsClauseSet(true);
     }
	]
    [<USING>
     using = setClauseList(true, info)
	 {
	     jjtThis.setUsing(using);
	 }
    ]
    [<UPDATE>
     (updateCount = intVal()
     |
     <STAR> 
     {	
        updateCount = 2;
     })
    ]
    {
        jjtThis.setUpdatingModelCount(updateCount);
        return jjtThis; 
    }
}

/*
name=set clause list
description=A list of value assignments.
example={code:sql}col1 = 'x', col2 = 'y' ...{code}
*/
SetClauseList setClauseList(boolean shortName, ParseInfo info) #SetClauseList :
{
	String element = null;
	Expression value = null;
}
{
	element = id(shortName?Boolean.TRUE:null)
    <EQ>
    value = expression(info)
    {
        ElementSymbol symbol = elementSymbol(element);
        jjtThis.addClause(setClause(symbol, value));
    }
	(<COMMA>
	 element = id(null)
     <EQ>
     value = expression(info)
     {
     	  symbol = elementSymbol(element);
          jjtThis.addClause(setClause(symbol, value));
     }
	)*
	{
		return jjtThis;
	}
}

SetClause setClause(ElementSymbol symbol, Expression value) #SetClause :
{
}
{
    {
        jjtThis.setSymbol(symbol);
        jjtThis.setValue(value);
        return jjtThis;
    }
}

/*
name=typed element list
description=A list of typed elements.
example={code:sql}col1 string, col2 integer ...{code}
*/
List<ProjectedColumn> createElementsWithTypes(ParseInfo info) :
{
	String element = null;
	ParsedDataType type = null;
	List<ProjectedColumn> elements = new ArrayList<ProjectedColumn>();
}
{
	 element = id(Boolean.TRUE)
	 type = parseDataType()
	 {
	    ProjectedColumn symbol = projectedColumn(element, type.getType());
		elements.add(symbol);
	 }
	 (LOOKAHEAD(2) <COMMA>
		element = id(Boolean.TRUE)
		type = parseDataType()
		{
	      symbol = projectedColumn(element, type.getType());
		  elements.add(symbol);
		}
	 )*
	 {
	 	return elements;
	 }
}

ProjectedColumn projectedColumn(String name, String type) #ProjectedColumn :
{
}
{
    {
        jjtThis.setName(name);
        jjtThis.setType(type);
        return jjtThis;
    }
}

/*
name=callable statement
description=A callable statement defined using JDBC escape syntax.
example={code:sql}{? = CALL proc}{code}
index=true
*/
StoredProcedure callableStatement(ParseInfo info) :
{
	StoredProcedure storedProcedure = newStoredProcedure();
	storedProcedure.setCallableStatement(true);
	String procName = null;
	Option option = null;
	SPParameter parameter = null;
	int parameterIndex = 1;
	List<Expression> values = null;
}
{
	<LBRACE>
	[<QMARK> <EQ>
	 {
	   parameter = new SPParameter(this, parameterIndex++, SPParameter.RETURN_VALUE, "return"); //$NON-NLS-1$
	   storedProcedure.setParameter(parameter);
	   storedProcedure.setCalledWithReturn(true);
	 }
	]
	<CALL> procName = id(null)
	{
		storedProcedure.setProcedureName(procName);
	}
		
	//parameters
		
	[<LPAREN>
	  (LOOKAHEAD(2)
        executeNamedParams(info, storedProcedure, parameterIndex)
        |      
        [values = expressionList(info) { convertToParameters(values, storedProcedure, parameterIndex); }]
      )
	 <RPAREN>
	]
	<RBRACE>			
	[option = option(info)
	 {
	   storedProcedure.setOption(option);
	 }
	]

	{
		return storedProcedure;
	}
}

/*
name=call statement
description=Executes the procedure with the given parameters.
example={code:sql}CALL proc('a', 1){code}
*/
StoredProcedure storedProcedure(ParseInfo info, int startIndex) :
{
	String procName = null;
	Option option = null;
	List<Expression> values = null;
	StoredProcedure storedProcedure = newStoredProcedure();
	SourceHint sourceHint = null;
}
{
    { if (versionAtLeast(Version.TEIID_8_4)) sourceHint = getSourceHint(this); }
	(
		(<EXEC> | <EXECUTE> | <CALL>)
		procName = id(null)
		{
			storedProcedure.setProcedureName(procName);
		}
		
		//parameters
		<LPAREN>
		
		(
		 LOOKAHEAD(2)
		 executeNamedParams(info, storedProcedure, startIndex)
		 | 		
		 [values = expressionList(info) { convertToParameters(values, storedProcedure, startIndex); }]
		)
		
		<RPAREN>
	)
	[option = option(info)
	 {
	   storedProcedure.setOption(option);
	 }
	]
	{
	    if (versionAtLeast(Version.TEIID_8_4))
	      storedProcedure.setSourceHint(sourceHint);

		return storedProcedure;
	}
}

StoredProcedure newStoredProcedure() #StoredProcedure :
{
}
{
    {
        return jjtThis;
    }
}

/*
name=named parameter list
description=A list of named parameters.
example={code:sql}param1 => 'x', param2 => 1{code}
*/
void executeNamedParams(ParseInfo info, StoredProcedure storedProcedure, int parameterIndex) :
{
	String name = null;
    Expression value = null;
	SPParameter parameter = null;
}
{
	{
		storedProcedure.setDisplayNamedParameters(true);
	}
	(name=id(null)
	 <EQ> [<GT>]
	 value = expression(info)
		{
			parameter = new SPParameter(this, parameterIndex++, value);
			parameter.setName(name);
			parameter.setParameterType(SPParameter.IN);
			storedProcedure.setParameter(parameter);
			parameter = null;
		}
		(	<COMMA>
		 	name=id(null)
	 		<EQ> [<GT>]
	 		value = expression(info)
			{
				parameter = new SPParameter(this, parameterIndex++, value);
				parameter.setName(name);
				parameter.setParameterType(SPParameter.IN);
				storedProcedure.setParameter(parameter);
				parameter = null;
			}
		)*
	)
}

/*
name=insert statement
description=Inserts values into the given target.
example={code:sql}INSERT INTO tbl (col1, col2) VALUES ('a', 1){code}
*/
Insert insert(ParseInfo info) #Insert :
{
	boolean merge = false;
	String group = null;
	List values = null;
	List<ElementSymbol> columns = null;
	Option option = null;
	QueryCommand query = null;
}
{
	(<INSERT>|<MERGE>{merge=true;}) <INTO>
	group = id(null)

	[LOOKAHEAD(<LPAREN> id(null)) columns = columnList(false, true) {
	  jjtThis.setVariables(columns);
	}]

	(
		( <VALUES> <LPAREN>
		  values = expressionList(info)
		  <RPAREN>
		  {
		    // Store each row of values
		    jjtThis.setValues(values);
		  }
		)
		|
		( query = queryExpression(info)
		  {
		    jjtThis.setQueryExpression(query);
		  }
		)
	)
	
	[ option = option(info)
	  {
	    jjtThis.setOption(option);
	  }
	]
	
	{
		// Store group
		jjtThis.setGroup(groupSymbol(group) );
		jjtThis.setMerge(merge);
		return jjtThis;
	}
}

/*
name=expression list
description=A list of expressions.
example={code:sql}col1, 'a', ...{code}
*/
ArrayList<Expression> expressionList(ParseInfo info) :
{
	ArrayList<Expression> rowVals = new ArrayList<Expression>(4);
	Expression value = null;
}
{
	value = expression(info)
	{
        rowVals.add(value);
	}
	(	<COMMA>
	    value = expression(info)
	    {
            rowVals.add(value);
	    }
	)*
	{
		return rowVals;
	}
}

/*
name=update statement
description=Update values in the given target.
example={code:sql}UPDATE tbl SET (col1 = 'a') WHERE col2 = 1{code}
*/
Update update(ParseInfo info) #Update :
{
	String group = null;
	SetClauseList setClauseList = null;
	Criteria criteria = null;
	Option option = null;
}
{
	<UPDATE>
	group = id(null)
	<SET>
	setClauseList = setClauseList(false, info)
	{
		jjtThis.setChangeList(setClauseList);
	}
	[ criteria = where(info) ]
	[option = option(info)
	  {
	   jjtThis.setOption(option);
	  }
	]
	{	
		// Store group
		jjtThis.setGroup(groupSymbol( group) );
		
		// Store optional criteria
		if(criteria != null) {
			jjtThis.setCriteria(criteria);
		}
		
		return jjtThis;
	}
}

/*
name=delete statement
description=Delete rows from the given target.
example={code:sql}DELETE FROM tbl WHERE col2 = 1{code}
*/
Delete delete(ParseInfo info) #Delete :
{
	String group = null;
	Criteria criteria = null;
	Option option = null;
}
{
	<DELETE> <FROM>
	group = id(null)
	[criteria = where(info)]
	[option = option(info)
	  {
	   jjtThis.setOption(option);
	  }
	]
	{
		jjtThis.setGroup(groupSymbol(group));
		jjtThis.setCriteria(criteria);
		        
        return jjtThis; 
	}
}

/*
name=query expression
description=A declarative query for data.
example={code:sql}SELECT * FROM tbl WHERE col2 = 1{code}
*/
QueryCommand queryExpression(ParseInfo info) :
{
    QueryCommand queryCommand = null;
	List<WithQueryCommand> withList = null;
	WithQueryCommand withQueryCommand = null;
	SourceHint sourceHint = null;
}
{
	[<WITH> withQueryCommand = withListElement(info)
	  {
	    withList = new LinkedList<WithQueryCommand>();
	    withList.add(withQueryCommand);
	  }
	  (	<COMMA>
	    withQueryCommand = withListElement(info)
	    {
            withList.add(withQueryCommand);
	    }
	  )*
	]
	{ if (versionAtLeast(Version.TEIID_8_4)) sourceHint = getSourceHint(this); }
	queryCommand = queryExpressionBody(info)
	{
		queryCommand.setWith(withList);

		if (versionAtLeast(Version.TEIID_8_4))
		  setSourceHint(sourceHint, queryCommand);

		return queryCommand;
	}
}

/*
name=with list element
description=A query expression for use in the enclosing query. 
example={code:sql}X (Y, Z) AS (SELECT 1, 2){code}
*/
WithQueryCommand withListElement(ParseInfo info) #WithQueryCommand :
{
   String name = null;
   List<ElementSymbol> columns = null;
   QueryCommand queryExpression = null;
}
{
	name = id(Boolean.FALSE)
	[ columns = columnList(true, true)] 
	<AS> <LPAREN> queryExpression = queryExpression(info) <RPAREN>
	{
	    jjtThis.setGroupSymbol(groupSymbol(name));
	    jjtThis.setColumns(columns);
	    jjtThis.setQueryExpression(queryExpression);
	    return jjtThis;
	}
}

/*
name=query expression body
description=The body of a query expression, which can optionally be ordered and limited.
example={code:sql}SELECT * FROM tbl ORDER BY col1 LIMIT 1{code}
*/
QueryCommand queryExpressionBody(ParseInfo info) :
{
    QueryCommand query = null;
    QueryCommand rightQuery = null;
    boolean all = false;
    Option option = null;
	OrderBy orderby = null;
	Limit limit = null;
	ISetQuery.Operation type;
}
{
	query = queryTerm(info) 
	(	(LOOKAHEAD( <UNION>, { getToken(2).kind != JOIN } ) <UNION> { type = ISetQuery.Operation.UNION; } | <EXCEPT> { type = ISetQuery.Operation.EXCEPT; }) 
 	 	[<ALL> { all = true; } | <DISTINCT>]
 	 	rightQuery = queryTerm(info)
 	   	{
 	   		query = setQuery(type, all, query, rightQuery);
    		all = false;
	   	}
	) *
    [orderby  = orderby(info) {query.setOrderBy( orderby );}]
    [limit    = limit(info) {query.setLimit( limit );}]
    [LOOKAHEAD(<OPTION>) option = option(info) { query.setOption(option);  }	]
    {
		return query;
    }    
}

/*
name=query term
description=Used to establish INTERSECT precedence.
example={code:sql}SELECT * FROM tbl{code}\n{code:sql}SELECT * FROM tbl1 INTERSECT SELECT * FROM tbl2{code}
*/
QueryCommand queryTerm(ParseInfo info) :
{
    QueryCommand query = null;
    QueryCommand rightQuery = null;
    boolean all = false;
}
{
	query=queryPrimary(info) 
	(	<INTERSECT> 
 		[<ALL> { all = true; } | <DISTINCT>] 
 		rightQuery=queryPrimary(info)
 		{
 	   		query = setQuery(ISetQuery.Operation.INTERSECT, all, query, rightQuery);
    		all = false;
 		}
	) *
    {
        return query;
    }
}    

SetQuery setQuery(ISetQuery.Operation operation, boolean all, QueryCommand leftQuery, QueryCommand rightQuery) #SetQuery :
{
}
{
    {
        jjtThis.setOperation(operation);
        jjtThis.setAll(all);
        jjtThis.setLeftQuery(leftQuery);
        jjtThis.setRightQuery(rightQuery);
        return jjtThis;
    }
}

/*
name=query primary
description=A declarative source of rows.
example={code:sql}TABLE tbl{code}\n{code:sql}SELECT * FROM tbl1{code}
*/
QueryCommand queryPrimary(ParseInfo info) :
{
    QueryCommand query = null;
    String name = null;
}
{
    (
        query=query(info) | 
        (<TABLE> name=id(null) {
           MultipleElementSymbol multipleElementSymbol = createASTNode(ASTNodes.MULTIPLE_ELEMENT_SYMBOL);

           Select select = createASTNode(ASTNodes.SELECT);
           select.setSymbols(Arrays.asList(multipleElementSymbol));

           UnaryFromClause unaryFromClause = createASTNode(ASTNodes.UNARY_FROM_CLAUSE);
           unaryFromClause.setGroup(groupSymbol(name));

           From from = createASTNode(ASTNodes.FROM);
           from.setClauses(Arrays.asList(unaryFromClause));
           
           Query q = createASTNode(ASTNodes.QUERY);
           q.setSelect(select);
           q.setFrom(from);
           query = q;
        }) |
        (<LPAREN> query=queryExpressionBody(info) <RPAREN>)
    )    
    {
        return query;
    }
}    

/*
name=query
description=A SELECT query.
example={code:sql}SELECT col1, max(col2) FROM tbl GROUP BY col1{code}
*/
Query query(ParseInfo info) #Query :
{
	Select   select         = null;
	From     from           = null;
	Into 	 into			= null;
	Criteria criteria       = null;
	GroupBy  groupBy        = null;
	Criteria having         = null;	
}
{
    select	  = select(info)
    [into	  = into(info)]
   	[from 	  = from(info)
   		
    [criteria = where(info)]
    [groupBy  = groupBy(info)]
    [having   = having(info)]]

	{
		// Build query from parsed pieces
		jjtThis.setSelect( select );
		jjtThis.setFrom( from );
		jjtThis.setInto( into );
		jjtThis.setCriteria(criteria);
		jjtThis.setGroupBy(groupBy);
		jjtThis.setHaving(having);

		return jjtThis;
  	}
}

/*
name=into clause
description=Used to direct the query into a table.  {note}This is deprecated.  Use INSERT INTO with a query expression instead.{note}
example={code:sql}INTO tbl{code}
*/
Into into(ParseInfo info) #Into :
{
	String groupID = null;
}
{	
	<INTO>
	(groupID=id(null))
	{
	    jjtThis.setGroup(groupSymbol(groupID));
		return jjtThis;
    }	
}

/*
name=select clause
description=The columns returned by a query.  Can optionally be distinct.
example={code:sql}SELECT *{code}\n{code:sql}SELECT DISTINCT a, b, c{code}
 */
Select select(ParseInfo info) #Select :
{
	boolean isDistinct = false;		// unless DISTINCT keyword in SELECT
	Expression symbol = null;
}
{
	<SELECT>
	[<ALL> | (<DISTINCT> {isDistinct=true;})]
	(<STAR>
		{
		    MultipleElementSymbol multipleElementSymbol = createASTNode(ASTNodes.MULTIPLE_ELEMENT_SYMBOL);
			jjtThis.addSymbol(multipleElementSymbol);
		}
	|
		(	symbol=selectSymbol(info)      { jjtThis.addSymbol(symbol); }
		    (	<COMMA>
				symbol=selectSymbol(info)  { jjtThis.addSymbol(symbol); }
			)*
		)
	)
	{
 		jjtThis.setDistinct(isDistinct);
		return jjtThis;
	}
}

ExpressionSymbol expressionSymbol() #ExpressionSymbol :
{
}
{
    {
        return jjtThis;
    }
}

/*
name=select sublist
description=An element in the select clause
example={code:sql}tbl.*{code}\n{code:sql}tbl.col AS x{code}
*/
Expression selectSymbol(ParseInfo info) :
{
	Expression symbol = null;	
}
{
	(
		symbol = selectExpression(info)
		|
		symbol = allInGroupSymbol()
	)
	{
		return symbol;
	}
}

/*
name=select derived column
description=A select clause item that selects a single column. {note}This is slightly different than a derived column in that the AS keyword is optional.{note}
example={code:sql}tbl.col AS x{code}
*/
Expression selectExpression(ParseInfo info) :
{
	Expression expression = null;
    String alias = null;
}
{
	(	
		// Expression
		expression=expression(info)	
		[[<AS>] alias=id(Boolean.FALSE) ]
	)
	{		
		if(alias != null) {
			return aliasSymbol(alias, expression);
		}
		return expression;
	}
}

/*
name=derived column
description=An optionally named expression.
example={code:sql}tbl.col AS x{code}
*/
DerivedColumn derivedColumn(ParseInfo info) #DerivedColumn :
{
	Expression expression = null;
    String alias = null;
}
{
	(	
		// Expression
		expression=expression(info)	
		[<AS> alias=id(Boolean.FALSE) ]
	)
	{
	    jjtThis.setAlias(alias);
	    jjtThis.setExpression(expression);
	    return jjtThis;
	}
}

/*
name=all in group 
description=A select sublist that can select all columns from the given group.
example={code:sql}tbl.*{code}
*/
MultipleElementSymbol allInGroupSymbol() #MultipleElementSymbol :
{
	Token allInGroupToken = null;
	
}
{
   	allInGroupToken = <ALL_IN_GROUP>    
    {
        String name = normalizeId(allInGroupToken.image.substring(0, allInGroupToken.image.length() - 2));
        jjtThis.setName(name);
		return jjtThis;
	}
}

/*
name=ordered aggreate function
description=An aggregate function that can optionally be ordered.
example={code:sql}XMLAGG(col1) ORDER BY col2{code}\n{code:sql}ARRAY_AGG(col1){code}
*/
AggregateSymbol orderedAgg(ParseInfo info) :
{
    Token t = null;
	Expression expression = null;
	OrderBy orderBy = null;
}
{
	(t=<XMLAGG>|t=<ARRAY_AGG>|t=<JSONARRAY_AGG>)
	<LPAREN>
	expression = expression(info)
	[ orderBy = orderby(info) ]
	<RPAREN>
	{
		String aggName = t.image.toUpperCase();
		AggregateSymbol	agg = newAggregateSymbol(aggName, false, new Expression[] {expression});
		agg.setOrderBy(orderBy);
		return agg;
	}
}

/*
name=text aggreate function
description=An aggregate function for creating separated value clobs.
example={code:sql}TEXTAGG (col1 as t1, col2 as t2 DELIMITER ',' HEADER){code}
*/
AggregateSymbol textAgg(ParseInfo info) :
{
	DerivedColumn expression = null;
	Character delimiter = null;
	Character quote = null;
	boolean header = false;	
	List<DerivedColumn> expressions = new ArrayList<DerivedColumn>();
	OrderBy orderBy = null;
	String encoding = null;
}
{
	<TEXTAGG> <LPAREN>
	[<FOR>] expression = derivedColumn(info)
	{
		expressions.add(expression);
	}
	(<COMMA>
		expression = derivedColumn(info)
		{
			expressions.add(expression);
		}
	)* 		
	[
	  <DELIMITER>
	  delimiter = charVal(info, "DELMITER")
	]
	[
	  <QUOTE>
	  quote = charVal(info, "QUOTE")
	]
	[
	  <HEADER>
	  { header = true; }
	]	
	[
	  <ENCODING>
	  encoding = id(null) 
	]
	[
		orderBy = orderby(info)
	]	
	<RPAREN>
	{
		TextLine tf = textLine();
		tf.setDelimiter(delimiter);
		tf.setQuote(quote);
		tf.setIncludeHeader(header);
		tf.setExpressions(expressions);
		tf.setEncoding(encoding);
		AggregateSymbol agg = newAggregateSymbol("TEXTAGG", false, new Expression[] {tf});
		agg.setOrderBy(orderBy);
		return agg;		
	}
}

/*
name=standard aggregate function
description=A standard aggregate function.
example={code:sql}COUNT(*){code}
*/
AggregateSymbol aggregateSymbol(ParseInfo info) :
{
	Token func = null;
	Token starToken = null;
    AggregateSymbol agg = null;
	boolean isDistinct = false;
	Expression expression = null;
}
{
	(
		// COUNT(*)
		LOOKAHEAD(3) (func = <COUNT>
			<LPAREN>
			starToken = <STAR>
			<RPAREN> )	
		| 
		// Remaining aggregates
		(	(<COUNT> | <SUM> | <AVG> | <MIN> | <MAX> | <EVERY> | <STDDEV_POP> | <STDDEV_SAMP> | <VAR_SAMP> | <VAR_POP> | <SOME> | <ANY>) {func = getToken(0);}
			<LPAREN>
			[ <DISTINCT> {isDistinct=true;} | <ALL>]
			expression = expression(info)
			<RPAREN>
		)
	)
	{
	    
		String name = func.image.toUpperCase();
		if(starToken == null) { 
			// Aggregate
			agg = newAggregateSymbol(name, isDistinct, new Expression[] {expression});
		} else {
			// COUNT(*)
			agg = newAggregateSymbol(name, false, null);
		}
		return agg;
	}
}

/*
name=analytic aggregate function
description=An analytic aggregate function.
example={code:sql}ROW_NUMBER(){code}
*/
AggregateSymbol analyticAggregateSymbol(ParseInfo info) :
{
	Token func = null;
}
{
	(<ROW_NUMBER> | <RANK> | <DENSE_RANK>) {func = getToken(0);}
	<LPAREN> <RPAREN>
	{
		return newAggregateSymbol(func.image.toUpperCase(), false, null);	
	}
}

/*
name=filter clause
description=An aggregate filter clause applied prior to accumulating the value.
example={code:sql}FILTER (WHERE col1='a'){code}
*/
Expression filterClause(ParseInfo info) :
{
	Expression condition = null;
}
{
	<FILTER> <LPAREN> <WHERE> condition = booleanPrimary(info) <RPAREN>
	{
		return condition;	
	}
}

/*
name=from clause
description=A query from clause containing a list of table references.
example={code:sql}FROM a, b{code}\n{code:sql}FROM a right outer join b, c, d join e".</p>{code}
*/
From from(ParseInfo info) #From :
{
	FromClause clause = null;
	List clauseList = new LinkedList();
}
{
	<FROM>
	(	clause = tableReference(info)
		{
			clauseList.add(clause);
			clause = null;
		}
		
		(<COMMA> clause = tableReference(info)
			{
				clauseList.add(clause);
				clause = null;
			}
		)*
	) 

	{
	    jjtThis.setClauses(clauseList);
		return jjtThis;
	}
}					

/*
name=table reference
description=An optionally escaped joined table.
example={code:sql}a{code}\n{code:sql}a inner join b{code}
*/
FromClause tableReference(ParseInfo info) :
{
	FromClause result = null;
}
{	
	(
		(	
			<ESCAPEDJOIN>
			result=joinedTable(info)
			<RBRACE>
		) 
		|
		result=joinedTable(info)
	)
	{
		return result;
	}
}	

/*
name=joined table
description=A table or join.
example={code:sql}a{code}\n{code:sql}a inner join b{code}
*/
FromClause joinedTable(ParseInfo info) :
{
	FromClause result = null;
	JoinPredicate jp = null;
}
{
	result = tablePrimary(info) /* this is a fix for left recursion in the spec */
	( LOOKAHEAD(2)	
		(	jp = crossJoin(info)
			| jp = qualifiedJoin(info)
		)
		{
			jp.setLeftClause(result);
			result = jp;
		}
	) *
	{
		return result;
	}
}

/*
name=cross join
description=A cross join.
example={code:sql}a CROSS JOIN b{code}
*/
JoinPredicate crossJoin(ParseInfo info) :
{
    JoinPredicate result = joinPredicate();
	FromClause clause = null;
}
{
	(	(<CROSS> {result.setJoinType(joinType(IJoinType.Types.JOIN_CROSS));}|<UNION> {result.setJoinType(joinType(IJoinType.Types.JOIN_UNION));})
    	<JOIN> 
		clause = tablePrimary(info) {result.setRightClause(clause);}
	)
	{
		return result;
	}
}

/*
name=qualified table
description=An INNER or OUTER join.
example={code:sql}a inner join b{code}
*/
JoinPredicate qualifiedJoin(ParseInfo info) :
{
	JoinPredicate result = joinPredicate();
	FromClause clause = null;
	Criteria onCriteria = null;
}
{
	(	[	(<RIGHT> {result.setJoinType(joinType(IJoinType.Types.JOIN_RIGHT_OUTER));} [<OUTER>])
    		| (<LEFT> {result.setJoinType(joinType(IJoinType.Types.JOIN_LEFT_OUTER));} [<OUTER>])
    		|  (<FULL> {result.setJoinType(joinType(IJoinType.Types.JOIN_FULL_OUTER));} [<OUTER>])
    		| <INNER>
		]
		<JOIN>
		clause = tableReference(info) {result.setRightClause(clause);}
		<ON>
		onCriteria = criteria(info) {result.setJoinCriteria(Criteria.separateCriteriaByAnd(onCriteria));}
	)
	{
		return result;
	}
}

JoinPredicate joinPredicate() #JoinPredicate :
{
}
{
    {
        return jjtThis;
    }
}

JoinType joinType(IJoinType.Types kind) #JoinType :
{
}
{
    {
        jjtThis.setKind(kind);
        return jjtThis;
    }
}

/*
name=table primary
description=A single source of rows.
example={code:sql}a{code}
*/
FromClause tablePrimary(ParseInfo info) :
{
	FromClause clause = null;
	Token lparen = null;
}
{
	(	LOOKAHEAD(2) clause = textTable(info)
	    |
	    LOOKAHEAD(2) clause = arrayTable(info)
	    |
	    clause = xmlTable(info)
	    |
	    LOOKAHEAD(2) clause = objectTable(info)
	    |
	    clause = unaryFromClause(info)
		|
		LOOKAHEAD(subqueryFromClause(info)) clause = subqueryFromClause(info) 
	    |			
		(lparen = <LPAREN> clause = joinedTable(info) 
		 {
			setFromClauseOptions(lparen, clause);
		 }
		 <RPAREN> 
		)
	)
	[	(<MAKEDEP>  { clause.setMakeDep(true); })
	 	| (<MAKENOTDEP> {  clause.setMakeNotDep(true); } )
	]
	{
		return clause;
	}
}

/*
name=xml serialize
description=Serializes an XML value.
example={code:sql}XMLSERIALIZE(col1 AS CLOB){code}
*/
XMLSerialize xmlSerialize(ParseInfo info) #XMLSerialize :
{
	Expression expr = null;
	Boolean doc = null;
	Token t = null;
	String enc = null;
	Boolean declr = null;
	String ver = null;
}
{
	<XMLSERIALIZE> <LPAREN>
	[LOOKAHEAD(1) (<DOCUMENT> {doc = true;}| <CONTENT> {doc = false;})]
	expr = expression(info)
	[
		<AS> (<STRING> | <VARCHAR> | <CLOB> | <VARBINARY> | <BLOB>) { t = getToken(0);}
	]
	[ <ENCODING> enc = id(null) ]
	[ <VERSION> ver = stringVal() ]
	[ ( <INCLUDING> {declr = true;}| <EXCLUDING> {declr = false;}) <XMLDECLARATION> ]
	<RPAREN>
	{
		jjtThis.setEncoding(enc);
		jjtThis.setVersion(ver);
		jjtThis.setDeclaration(declr);
		jjtThis.setExpression(expr);
		if (doc != null) {
			jjtThis.setDocument(doc);
		}
		if (t != null) {
			jjtThis.setTypeString(t.image);
		}
		return jjtThis;
	}
}

/*
name=array table
description=The ARRAYTABLE table function creates tabular results from arrays.  It can be used as a nested table reference.
example={code:sql}ARRAYTABLE (col1 COLUMNS x STRING) AS y{code}
*/
ArrayTable arrayTable(ParseInfo info) # ArrayTable :
{
	Expression array = null;
	List<ProjectedColumn> columns;
	String aliasID = null;
}
{
 	<ARRAYTABLE> <LPAREN> array = valueExpressionPrimary(info)
 	<COLUMNS>
 	columns = createElementsWithTypes(info) 
 	<RPAREN>
 	[<AS>] aliasID=id(Boolean.FALSE)
 	{
 		jjtThis.setArrayValue(array);
 		jjtThis.setColumns(columns);
 		jjtThis.setName(aliasID);
 		return jjtThis;
 	}
}

/*
name=text table
description=The TEXTTABLE table function creates tabular results from text.  It can be used as a nested table reference.
example={code:sql}TEXTTABLE (file COLUMNS x STRING) AS y{code}
*/
TextTable textTable(ParseInfo info) #TextTable :
{
	Expression file = null;
	TextColumn column = null;
	List<TextColumn> columns = new ArrayList<TextColumn>();
	boolean useRowDelimiter = true;
	Character delimiter = null;
	boolean escape = false;
	Character quote = null;
	Integer header = null;
	Integer skip = null;
	String selector = null;
	String aliasID = null;
}
{
 	<TEXTTABLE> <LPAREN> file = commonValueExpression(info)
 	[
	  <SELECTOR>
	  selector = stringVal()
	]
 	<COLUMNS>
 	column = textColumn(info)
	{
		columns.add(column);
	} 
	(<COMMA>
		column = textColumn(info)
		{
			columns.add(column);
		}
	)* 
	[
	  <NO> <ROW> <DELIMITER>
	  {
	    useRowDelimiter = false;
	  }
	]
	[
	  <DELIMITER>
	  delimiter = charVal(info, "DELMITER")
	]
	[
	  (<ESCAPE> quote = charVal(info, "ESCAPE") ) { escape = true; }
	  |
	  (<QUOTE> quote = charVal(info, "QUOTE") )
	]
	[
	  <HEADER>
	  [
	  	header = intVal()
	  ]
	  {
	  	if (header == null) {
	  		header = 1;
	  	}
	  }
	]
	[
	  <SKIP_KEYWORD>
	  skip = intVal()
	]
 	<RPAREN>
 	[<AS>] aliasID=id(Boolean.FALSE)
 	{
 		jjtThis.setFile(file);
 		jjtThis.setColumns(columns);
 		jjtThis.setDelimiter(delimiter);
 		jjtThis.setEscape(escape);
 		jjtThis.setHeader(header);
 		jjtThis.setSkip(skip);
 		jjtThis.setName(aliasID);
 		jjtThis.setQuote(quote);
 		jjtThis.setUsingRowDelimiter(useRowDelimiter);
 		jjtThis.setSelector(selector);
 		return jjtThis;
 	}
}

/*
name=text table column
description=A text table column.
example={code:sql}x INTEGER WIDTH 6{code}
*/
TextColumn textColumn(ParseInfo info) #TextColumn :
{
	String name = null;
	ParsedDataType datatype = null;
	Integer width = null;
	boolean noTrim = false;
	String selector = null;
	Integer position = null;
}
{
	name = id(Boolean.TRUE)
	datatype = parseDataType()
	[ 
	  	<WIDTH>
	  	width = intVal() 
	  	[ LOOKAHEAD(2)
		  	<NO> <TRIM>
		  	{
				noTrim = true;	  	
		  	}
		]
	]
	[ 
	  	<SELECTOR>
	    selector = stringVal()
	  	position = intVal() 
	]
	{
	    jjtThis.setName(name);
	    jjtThis.setType(datatype.getType());
	    jjtThis.setWidth(width);
	    jjtThis.setNoTrim(noTrim);
		jjtThis.setSelector(selector);
		jjtThis.setPosition(position);
		return jjtThis;
	}
}

/*
name=xml query
description=Executes an XQuery to return an XML result.
example={code:sql}XMLQUERY('<a>...</a>' PASSING doc){code}
*/
XMLQuery xmlQuery(ParseInfo info) #XMLQuery :
{
	String xquery = null;
	XMLNamespaces xmlNamespaces = null;
	DerivedColumn passing = null;
	ArrayList<DerivedColumn> passingValues = new ArrayList<DerivedColumn>();
	Boolean content = null;
	Boolean empty = null;
}
{
 	<XMLQUERY> <LPAREN> 
 	[ xmlNamespaces = xmlNamespaces(info) <COMMA> ]
 	xquery = stringVal()
 	[ 
 	  <PASSING>
 	  passing = derivedColumn(info)
 	  {
 	  	passingValues.add(passing);
 	  	passing.setPropagateName(false);
 	  }
 	  (<COMMA>
 	    passing = derivedColumn(info)
 	    {
 	  	  passingValues.add(passing);
 	  	  passing.setPropagateName(false);
 	    }
 	  )*
 	]

 	[
 	  (<NULL> { empty = false; }
 	   |
 	   <EMPTY> { empty = true; }
 	  )
 	  <ON> <EMPTY>
 	]
 	<RPAREN>
 	{
 		jjtThis.setXquery(xquery);
 		jjtThis.setNamespaces(xmlNamespaces);
 		jjtThis.setPassing(passingValues);
 		jjtThis.setEmptyOnEmpty(empty);
 		return jjtThis;
 	}
}

/*
name=object table
description=Returns table results by processing a script.
example={code:sql}OBJECTTABLE('z' PASSING val AS z COLUMNS col OBJECT 'teiid_row') AS X{code}
*/
ObjectTable objectTable(ParseInfo info) #ObjectTable :
{
	String rowScript = null;
	String lang = null;
	DerivedColumn passing = null;
	ArrayList<DerivedColumn> passingValues = new ArrayList<DerivedColumn>();
	ObjectColumn column = null;
	ArrayList<ObjectColumn> columns = new ArrayList<ObjectColumn>();
	String aliasID = null;
}
{
 	<OBJECTTABLE> <LPAREN> 
 	[<LANGUAGE> lang = stringVal()]
 	rowScript = stringVal()
 	[ 
 	  <PASSING>
 	  passing = derivedColumn(info)
 	  {
 	  	passingValues.add(passing);
 	  	passing.setPropagateName(false);
 	  }
 	  (<COMMA>
 	    passing = derivedColumn(info)
 	    {
 	  	  passingValues.add(passing);
 	  	  passing.setPropagateName(false);
 	    }
 	  )*
 	]
    <COLUMNS>
    column = objectColumn(info)
    {
      columns.add(column);
    }
    (<COMMA>
      column = objectColumn(info)
      {
        columns.add(column);
      }
    )*
 	<RPAREN>
 	[<AS>] aliasID=id(Boolean.FALSE)
 	{
 		jjtThis.setScriptingLanguage(lang);
 		jjtThis.setRowScript(rowScript);
 		jjtThis.setPassing(passingValues);
 		jjtThis.setColumns(columns);
 		jjtThis.setName(aliasID);
 		return jjtThis;
 	}
}

/*
name=object table column
description=object table column.
example={code:sql}y integer 'teiid_row_number'{code}
*/
ObjectColumn objectColumn(ParseInfo info) #ObjectColumn :
{
	String name = null;
	ParsedDataType datatype = null;
	String path = null;
	Expression defaultExpr = null;
}
{
	name = id(null)
    datatype = parseDataType()
	path = stringVal()    
    [
   	  <DEFAULT_KEYWORD> defaultExpr = expression(info)
	]
	{
	  jjtThis.setName(name);
	  jjtThis.setType(datatype.getType());
	  jjtThis.setPath(path);
	  jjtThis.setDefaultExpression(defaultExpr);
	  return jjtThis;
	}
}


/*
name=xml table
description=Returns table results by processing an XQuery.
example={code:sql}XMLTABLE('/a/b' PASSING doc COLUMNS col XML PATH '.') AS X{code}
*/
XMLTable xmlTable(ParseInfo info) #XMLTable :
{
	String xquery = null;
	XMLNamespaces xmlNamespaces = null;
	DerivedColumn passing = null;
	ArrayList<DerivedColumn> passingValues = new ArrayList<DerivedColumn>();
	XMLColumn column = null;
	ArrayList<XMLColumn> columns = new ArrayList<XMLColumn>();
	String aliasID = null;
}
{
 	<XMLTABLE> <LPAREN> 
 	[ xmlNamespaces = xmlNamespaces(info) <COMMA> ]
 	xquery = stringVal()
 	[ 
 	  <PASSING>
 	  passing = derivedColumn(info)
 	  {
 	  	passingValues.add(passing);
 	  	passing.setPropagateName(false);
 	  }
 	  (<COMMA>
 	    passing = derivedColumn(info)
 	    {
 	  	  passingValues.add(passing);
 	  	  passing.setPropagateName(false);
 	    }
 	  )*
 	]
 	[
 	  <COLUMNS>
 	  column = xmlColumn(info)
 	  {
 	    columns.add(column);
 	  }
 	  (<COMMA>
 	    column = xmlColumn(info)
 	    {
 	      columns.add(column); 	    
 	    }
 	  )*
 	]
 	<RPAREN>
 	[<AS>] aliasID=id(Boolean.FALSE)
 	{
 		jjtThis.setXquery(xquery);
 		jjtThis.setNamespaces(xmlNamespaces);
 		jjtThis.setPassing(passingValues);
 		jjtThis.setColumns(columns);
 		jjtThis.setName(aliasID);
 		return jjtThis;
 	}
}

/*
name=xml table column
description=XML table column.
example={code:sql}y FOR ORDINALITY{code}
*/
XMLColumn xmlColumn(ParseInfo info) #XMLColumn :
{
	String name = null;
	ParsedDataType datatype = null;
	String path = null;
	Expression defaultExpr = null;
}
{
	name = id(null)
	{
	   jjtThis.setName(name);
	}

	((
	  <FOR> <ORDINALITY> 
	  {
	    jjtThis.setOrdinal(true);
	    return jjtThis;
	  }	
	) | (
      datatype = parseDataType()
      [
	    <DEFAULT_KEYWORD> defaultExpr = expression(info)
	  ]
      [ 
	    <PATH> path = stringVal()
	  ]
	  {
	    jjtThis.setType(datatype.getType());
	    jjtThis.setPath(path);
	    jjtThis.setDefaultExpression(defaultExpr);
	    return jjtThis;
	  }
	))
}

/*
name=unsigned integer
description=An unsigned interger value.
example={code:sql}12345{code}
*/
int intVal() :
{
	Token t;
}
{
	t = <UNSIGNEDINTEGER>
	{
		try { 
			return Integer.valueOf(t.image); 				
		} catch(NumberFormatException e1) { 
			throw new ParseException(Messages.getString(Messages.TeiidParser.Integer_parse, t.image));
		}
	}
}

/*
name=table subquery
description=A table defined by a subquery.
example={code:sql}(SELECT * FROM tbl) AS x{code}
*/
SubqueryFromClause subqueryFromClause(ParseInfo info) #SubqueryFromClause :
{
    String aliasID = null;
	Command command = null;
	Token lparen = null;
	boolean table = false;
}
{	
	[
		(<TABLE>|<LATERAL>) { table = true; }
	]
	lparen = <LPAREN> 
	( command = queryExpression(info) |
      command = storedProcedure(info, 1) )
	<RPAREN>
	[<AS>]
	aliasID = id(Boolean.FALSE)
	
	{
	    jjtThis.setName(aliasID);
	    jjtThis.setCommand(command);
        setFromClauseOptions(lparen, jjtThis);
        jjtThis.setTable(table);
        return jjtThis;
    }	
}	
		
/*
name=table name
description=A table named in the FROM clause.
example={code:sql}tbl AS x{code}
*/
UnaryFromClause unaryFromClause(ParseInfo info) #UnaryFromClause :
{
	GroupSymbol group = null;
	String groupID = null;
	Token groupToken = null;
    String aliasID = null;
}
{	
	(groupID=id(null) {groupToken = getToken(0);} [[<AS>] aliasID=id(Boolean.FALSE)])
	{
    	if(aliasID != null) {		
            group = groupSymbol(aliasID, groupID);
        } else {
			group = groupSymbol(groupID);
        }
        jjtThis.setGroup(group);
        setFromClauseOptions(groupToken, jjtThis);
		return jjtThis;
    }	
}		

/*
name=where clause
description=Specifies a search condition
example={code:sql}WHERE x = 'a'{code}
*/
Criteria where(ParseInfo info) :
{
    Criteria criteria = null;
}
{
    <WHERE>
	criteria = criteria(info)
	
	{
		return criteria;
	}
}

/*
name=condition
description=A boolean expression.
*/
Criteria criteria(ParseInfo info) #Criteria :
{
}
{	
	jjtThis = compoundCritOr(info)

	{
		return jjtThis;
	}
}

/*
name=boolean value expression
description=An optionally ORed boolean expression.
*/
Criteria compoundCritOr(ParseInfo info) :
{
    ArrayList logicList = null;
    Criteria logicPart = null;
    Criteria logicPart1 = null;
}
{
	logicPart=compoundCritAnd(info) 
	(<OR> logicPart1=compoundCritAnd(info) {
        if (logicList == null) {
          logicList = new ArrayList(2);
          logicList.add(logicPart);
        }

        logicList.add(logicPart1);
	  }
	)*

	{
		if(logicList == null) {
			return logicPart;
		} else {
         	CompoundCriteria cc = createASTNode(ASTNodes.COMPOUND_CRITERIA);
         	cc.setOperator(CompoundCriteria.OR);
         	cc.setCriteria(logicList);
         	return cc;
		}
	}
}

/*
name=boolean term
description=An optional ANDed boolean factor.
*/
Criteria compoundCritAnd(ParseInfo info) #CompoundCriteria :
{
    ArrayList logicList = null;
    Criteria logicPart = null;
    Criteria logicPart1 = null;
}
{
	logicPart=notCrit(info) 		
	(<AND> logicPart1=notCrit(info) {
        if (logicList == null) {
          logicList = new ArrayList(2);
          logicList.add(logicPart);
        }

        logicList.add(logicPart1);
      }
    )*

	{
   		if(logicList == null) {
			return logicPart;
		} else {
		    jjtThis.setOperator(CompoundCriteria.AND);
		    jjtThis.setCriteria(logicList);
         	return jjtThis;
		}	
	}
}

/*
name=boolean factor
description=A boolean factor.
example={code:sql}NOT x = 'a'{code}
*/
Criteria notCrit(ParseInfo info) #NotCriteria :
{
    Expression ex = null;
	boolean isNot = false;
}
{
	[<NOT> {isNot=true;}] 
	ex=booleanPrimary(info)

	{
	    Criteria crit = null;
	    if (ex instanceof Criteria) {
	      crit = (Criteria)ex;
	    } else {
	      crit = expressionCriteria(ex);
	    }
  		if(isNot) {
  		    jjtThis.setCriteria(crit);
  			return jjtThis;
		} else {
			return crit;
		}
	}
}

/*
name=boolean primary
description=A boolean predicate or simple expression.
example={code:sql}col LIKE 'a%'{code}
*/
Expression booleanPrimary(ParseInfo info) :
{
    Expression ex = null;
}
{
    (
	    (ex = commonValueExpression(info)
	     [(
			LOOKAHEAD(2) ex=betweenCrit(info, ex) |
			LOOKAHEAD(2) ex=matchCrit(info, ex) |
			LOOKAHEAD(2) ex=regexMatchCrit(info, ex) |
			ex=setCrit(info, ex) |
			ex=isNullCrit(info, ex) |
			LOOKAHEAD(operator() (<ANY>|<SOME>|<ALL>) subquery(info)) ex=subqueryCompareCriteria(info, ex) |
			ex=compareCrit(info, ex)
	     )]
	    )
	    |
		ex=existsCriteria(info)
    )
	{
		return ex;
	}
}

/*
name=comparison operator
description=A comparison operator.
example={code:sql}={code}
*/
Token operator() :
{
	Token operator = null;
}
{
	(operator=<EQ> |
    operator=<NE> |
    operator=<NE2> |
    operator=<LT> |
    operator=<LE> |
    operator=<GT> |
    operator=<GE>
    )
    {
    	return operator;
    }
}

/*
name=comparison predicate
description=A value comparison.
example={code:sql}= 'a'{code}
*/
CompareCriteria compareCrit(ParseInfo info, Expression expression) #CompareCriteria :
{
    Expression value = null;
    Token operator = null;
}
{
	operator=operator()
	value=commonValueExpression(info)

	{
		// Set left expression
		jjtThis.setLeftExpression(expression);

		jjtThis.setOperator(Operator.getOperator(getVersion(), operator.image));
		
		// Set value
		jjtThis.setRightExpression(value);

	    return jjtThis;
	}
}

/*
name=subquery
description=A subquery.
example={code:sql}(SELECT * FROM tbl){code}
*/
QueryCommand subquery(ParseInfo info) :
{
    QueryCommand subquery = null;
    StoredProcedure proc = null;
}
{
	<LPAREN>
	( subquery = queryExpression(info) | 
	 (
		proc = storedProcedure(info, 1) //deprecated	  
	 )
	 {
	    MultipleElementSymbol multipleElementSymbol = createASTNode(ASTNodes.MULTIPLE_ELEMENT_SYMBOL);

        Select select = createASTNode(ASTNodes.SELECT);
        select.setSymbols(Arrays.asList(multipleElementSymbol));

        SubqueryFromClause subqueryFromClause = createASTNode(ASTNodes.SUBQUERY_FROM_CLAUSE);
        subqueryFromClause.setName("x");
        subqueryFromClause.setCommand(proc);

        From from = createASTNode(ASTNodes.FROM);
        from.setClauses(Arrays.asList(subqueryFromClause));

        Query query = createASTNode(ASTNodes.QUERY);
        query.setSelect(select);
        query.setFrom(from);
        subquery = query;
	 }
	)
	<RPAREN>
	{
		return subquery;
	}
}

/*
name=quantified comparison predicate
description=A subquery comparison.
example={code:sql}= ANY (SELECT col FROM tbl){code}
*/
SubqueryCompareCriteria subqueryCompareCriteria(ParseInfo info, Expression expression) #SubqueryCompareCriteria :
{
    QueryCommand subquery = null;
    Token operator = null;
    Token quantifier = null;
    
}
{
	operator=operator()
	( quantifier=<ANY> |
	  quantifier=<SOME> |
	  quantifier=<ALL>
	)
	subquery = subquery(info)

	{
	    jjtThis.setLeftExpression(expression);
	    jjtThis.setCommand(subquery);
    
		// Set operator
		jjtThis.setOperator(Operator.getOperator(getVersion(), operator.image));

		// Set predicate quantifier
		if(quantifier.image.equalsIgnoreCase("any")) { //$NON-NLS-1$
			jjtThis.setPredicateQuantifier(PredicateQuantifier.ANY);
		} else if(quantifier.image.equalsIgnoreCase("some")) { //$NON-NLS-1$
			jjtThis.setPredicateQuantifier(PredicateQuantifier.SOME);
		} else if(quantifier.image.equalsIgnoreCase("all")) { //$NON-NLS-1$
			jjtThis.setPredicateQuantifier(PredicateQuantifier.ALL);
		} 

	    return jjtThis;
   	}
}

/*
name=match predicate
description=Matches based upon a pattern.
example={code:sql}LIKE 'a_'{code}
*/
MatchCriteria matchCrit(ParseInfo info, Expression expression) #MatchCriteria :
{
    Character esc = null;
	Expression value = null;
	boolean negated = false;
	boolean similar = false;
	Token t = null;
}
{
	[<NOT> {negated = true;}]
    (<LIKE>|(<SIMILAR> <TO> {similar = true;}))
    {t = getToken(1);}
	value=commonValueExpression(info)
    [   <ESCAPE> esc = charVal(info, "LIKE/SIMILAR TO ESCAPE") |
        (<LBRACE> <ESCAPE> esc = charVal(info, "LIKE/SIMILAR TO ESCAPE") <RBRACE>) 
    ]
	{
	    jjtThis.setLeftExpression(expression);
	    jjtThis.setRightExpression(value);
		jjtThis.setNegated(negated);
	    if(esc != null) {
			jjtThis.setEscapeChar(esc.charValue());
		} else if (!similar && value instanceof Constant) {
			if (t.image != null && t.image.startsWith("E'")) {
				jjtThis.setEscapeChar('\\');
			}
		}
		jjtThis.setMode(similar?MatchMode.SIMILAR:MatchMode.LIKE);
		return jjtThis;
   	}
}

/*
name=like regex predicate
description=A regular expression match.
example={code:sql}LIKE_REGEX 'a.*b'{code}
*/
MatchCriteria regexMatchCrit(ParseInfo info, Expression expression) :
{
	Expression value = null;
	boolean negated = false;
}
{
	[<NOT> {negated = true;}]
    <LIKE_REGEX>
	value=commonValueExpression(info)
	{
    	MatchCriteria matchcriteria = createASTNode(ASTNodes.MATCH_CRITERIA);
    	matchcriteria.setLeftExpression(expression);
    	matchcriteria.setRightExpression(value);
		matchcriteria.setNegated(negated);
		matchcriteria.setMode(MatchMode.REGEX);
		return matchcriteria;
   	}
}

/*
name=character
description=A single character.
example={code:sql}'a'{code}
*/
Character charVal(ParseInfo info, String type) :
{
	String escStr = null;
}
{
	escStr=stringVal()
	{
		if (escStr.length() != 1) {
			throw new ParseException(Messages.getString(Messages.TeiidParser.char_val));
		}
		return Character.valueOf(escStr.charAt(0));
	}
}

/*
name=between predicate
description=A comparison between two values.
example={code:sql}BETWEEN 1 AND 5{code}
*/
BetweenCriteria betweenCrit(ParseInfo info, Expression expression) #BetweenCriteria :
{
	Expression lowerExpression = null, upperExpression = null;
	boolean negated = false;
}
{
	[<NOT> {negated=true;}]
	<BETWEEN>
	lowerExpression = commonValueExpression(info)
	<AND>
	upperExpression = commonValueExpression(info)
	
	{
		jjtThis.setExpression(expression);
		jjtThis.setLowerExpression(lowerExpression);
		jjtThis.setUpperExpression(upperExpression);
		jjtThis.setNegated(negated);
		return jjtThis;
	}
}

/*
name=is null predicate
description=A null test.
example={code:sql}IS NOT NULL{code}
*/
IsNullCriteria isNullCrit(ParseInfo info, Expression expression) #IsNullCriteria :
{
	boolean negated = false;
}
{
	<IS>
	[<NOT> {negated = true;}]
	<NULL>

	{
	   jjtThis.setExpression(expression);
	   jjtThis.setNegated(negated);
	   return jjtThis;
	}
}

/*
name=in predicate
description=A comparison with multiple values.
example={code:sql}IN (1, 5){code}
*/
AbstractSetCriteria setCrit(ParseInfo info, Expression expression) :
{
	Expression value = null;
	List setList = new ArrayList();
	QueryCommand command = null;
	SubqueryHint hint = null;
	boolean negated = false;
	AbstractSetCriteria criteria = null;
}
{
	[<NOT> {negated = true;}]
	<IN>
	(
		LOOKAHEAD(subquery(info)) { hint = getSubqueryHint(getToken(1)); } (command = subquery(info)) |
		(
			<LPAREN>
			value = commonValueExpression(info)
			{
				setList.add(value);
			}
			( <COMMA>
				value = commonValueExpression(info)
				{
					setList.add(value);
				}
			)*
			<RPAREN>
		)
	)
	{
	    if (command != null) {
			SubquerySetCriteria ssc = subquerySetCriteria(expression, command);
			ssc.setSubqueryHint(hint);
			criteria = ssc;
		} else {
		    criteria = setCriteria(expression, setList);
		}
	    criteria.setNegated(negated);
	    return criteria;
   	}
}

SubquerySetCriteria subquerySetCriteria(Expression expression, QueryCommand command) #SubquerySetCriteria :
{
}
{
    {
        jjtThis.setExpression(expression);
        jjtThis.setCommand(command);
        return jjtThis;
    }
}

SetCriteria setCriteria(Expression expression, List<Expression> values) #SetCriteria :
{
}
{
    {
        jjtThis.setExpression(expression);
        jjtThis.setValues(values);
        return jjtThis;
    }
}

/*
name=exists predicate
description=A test if rows exist.
example={code:sql}EXISTS (SELECT col FROM tbl){code}
*/
ExistsCriteria existsCriteria(ParseInfo info) #ExistsCriteria :
{
    QueryCommand subquery = null;
    SubqueryHint hint = null;
}
{
	<EXISTS> { hint = getSubqueryHint(getToken(1)); } 
	subquery = subquery(info)

	{
	    jjtThis.setCommand(subquery);
		jjtThis.setSubqueryHint(hint);
	    return jjtThis;
   	}
}

/*
name=group by clause
description=Defines the grouping columns
example={code:sql}GROUP BY col1, col2{code}
*/
GroupBy groupBy(ParseInfo info) #GroupBy :
{
	List<Expression> expressions = null;
}
{
	<GROUP> <BY>
    expressions = expressionList(info)	
	{
	    jjtThis.setSymbols(expressions);
		return jjtThis;
	}
}

/*
name=having clause
description=Search condition applied after grouping.
example={code:sql}HAVING max(col1) = 5{code}
*/
Criteria having(ParseInfo info) :
{
    Criteria criteria = null;
}
{
    <HAVING>
	criteria = criteria(info)
	{
		return criteria;
	}
}

/*
name=order by clause
description=Specifices row ordering.
example={code:sql}ORDER BY x, y DESC{code}
*/
OrderBy orderby(ParseInfo info) #OrderBy :
{
    OrderByItem item = null;
}
{
	<ORDER> <BY>
	item = sortSpecification(info)
	{
    	jjtThis.getOrderByItems().add(item);
	}
	(<COMMA>
		item = sortSpecification(info)
		{
    		jjtThis.getOrderByItems().add(item);
		}
	)*
	{
	    return jjtThis;
	}
}

/*
name=sort specification
description=Defines how to sort on a particular expression
example={code:sql}col1 NULLS FIRST{code}
*/
OrderByItem sortSpecification(ParseInfo info) #OrderByItem :
{
    Expression ex = null;
    boolean ascending = true;
    NullOrdering nullOrdering = null;    
}
{
	ex=sortKey(info) 
	[<ASC> | <DESC> {ascending=false;}]
	[<NULLS> (<FIRST> {nullOrdering=NullOrdering.FIRST;} | <LAST> {nullOrdering=NullOrdering.LAST;})] 
	{
	    jjtThis.setSymbol(ex);
	    jjtThis.setAscending(ascending);
    	if (nullOrdering != null) {
    		jjtThis.setNullOrdering(nullOrdering);
    	}
    	return jjtThis;
	}
}

/*
name=sort key
description=A sort expression.
example={code:sql}col1{code}
*/
Expression sortKey(ParseInfo info) :
{
    Expression ex = null;
}
{
    ex=expression(info)
    {
        //legacy support check for positional constants
        if (ex instanceof Constant) {
        	boolean valid = false;
            Constant c = (Constant)ex;
            if (c.getValue() instanceof Integer) {
                Integer val = (Integer)c.getValue();
                valid = val.intValue() > 0;
            }      
            if (!valid) {
            	throw new ParseException(Messages.getString(Messages.TeiidParser.non_position_constant, ex));
            }
        } 
        return ex;
    }
}

/*
name=integer parameter
description=A literal integer or parameter reference to an integer.
example={code:sql}?{code}
*/
Expression intParam(ParseInfo info) :
{
   Integer val = null;
}
{
   (val = intVal() | <QMARK>)
   {
     if (val == null) {
       return reference(info.incrementReferenceCount());
     } 
     return constant(val, DataTypeManagerService.DefaultDataTypes.INTEGER);
   }
}

/*
name=limit clause
description=Limits and/or offsets the resultant rows.
example={code:sql}LIMIT 2{code}
*/
Limit limit(ParseInfo info) #Limit :
{
	Expression limit = null;
	Expression offset = null;
	Token t = null;
}
{
    {t = getToken(1);}
    ((<LIMIT> offset = intParam(info)
	[<COMMA> limit = intParam(info)])
	{
	  if (limit == null) {
	    limit = offset;
	    offset = null;
	  }
	}
	|
	(<OFFSET> offset = intParam(info) (<ROW>|<ROWS>) 
    [limit = fetchLimit(info)])
    |
    (limit = fetchLimit(info)))
	{
	    jjtThis.setOffset(offset);
	    jjtThis.setRowLimit(limit);
		jjtThis.setStrict(!isNonStrictHint(t));
		return jjtThis;
	}
}

/*
name=fetch clause
description=ANSI limit.
example={code:sql}FETCH FIRST 1 ROWS ONLY{code}
*/
Expression fetchLimit(ParseInfo info) :
{
    Expression limit = null;
}
{
    <FETCH> (<FIRST> | <NEXT>) [limit = intParam(info)] (<ROW>|<ROWS>) <ONLY>
    {
      if (limit == null) {
        return constant(1, DataTypeManagerService.DefaultDataTypes.INTEGER); 
      }
      return limit;
    }
}

/*
name=option clause
description=Specifies query options.
example={code:sql}OPTION MAKEDEP tbl{code}
*/
Option option(ParseInfo info) #Option :
{
	String id = null;
	Token nocache = null;
}
{
	<OPTION>
	(
		<MAKEDEP>		
		id=id(null)
		{
            jjtThis.addDependentGroup(id);
		}
		(<COMMA>
			id=id(null)
			{
                jjtThis.addDependentGroup(id);
	        }
		)* |
		<MAKENOTDEP>		
		id=id(null)
		{
            jjtThis.addNotDependentGroup(id);
		}
		(<COMMA>
			id=id(null)
			{
                jjtThis.addNotDependentGroup(id);
	        }
		)* |
		
		nocache	= <NOCACHE>		
		[id=id(null)
		{
            jjtThis.addNoCacheGroup(id);
		}
		(<COMMA>
			id=id(null)
			{
                jjtThis.addNoCacheGroup(id);
	        }
		)*]
	)*

	{
		jjtThis.setNoCache( (nocache != null) );
		return jjtThis;
	}
}

/*
name=expression
description=A value.
example={code:sql}col1{code}
*/
Expression expression(ParseInfo info) :
{
  Expression expression = null;
}
{
    expression = criteria(info)
	{
		if (expression instanceof ExpressionCriteria) {
			return ((ExpressionCriteria)expression).getExpression();
		}
		return expression;
	}
}

/*
name=common value expression
description=Establishes the precedence of concat.
example={code:sql}'a' || 'b'{code}
*/
Expression commonValueExpression(ParseInfo info) :
{
	Expression leftExpression = null;
	Expression rightExpression = null;
}
{
	(	leftExpression=plusExpression(info)
		(
			<CONCAT_OP>
			rightExpression=plusExpression(info)
			{
			    Function function = createASTNode(ASTNodes.FUNCTION);
			    function.setName("||"); //$NON-NLS-1$
			    function.setArgs(new Expression[] {leftExpression, rightExpression});
			    leftExpression = function;
				rightExpression = null;
			}
		)*
	)

	{
		return leftExpression;
	}
}

/*
name=numeric value expression
example={code:sql}1 + 2{code}
*/
Expression plusExpression(ParseInfo info) :
{
	Expression leftExpression = null;
	Expression rightExpression = null;
	String operator = null;
}
{
	(	leftExpression=timesExpression(info)
		(
			operator=plusMinus()
			rightExpression=timesExpression(info)
			{
			    Function function = createASTNode(ASTNodes.FUNCTION);
			    function.setName(operator);
			    function.setArgs(new Expression[] { leftExpression, rightExpression });
			    leftExpression = function;
				rightExpression = null;
				operator = null;
			}
		)*
	)

	{
		return leftExpression;
	}
}

/*
name=plus or minus
description=The + or - operator.
example={code:sql}+{code}
*/
String plusMinus() :
{
}
{
	(<PLUS> | <MINUS>)
	{
		return getToken(0).image;
	}
}

/*
name=term
description=A numeric term
example={code:sql}1 * 2{code}
*/
Expression timesExpression(ParseInfo info) :
{
	Expression leftExpression = null;
	Expression rightExpression = null;
	String operator = null;
}
{
	(	leftExpression=valueExpressionPrimary(info)
		(
			operator=timesOperator()
			rightExpression=valueExpressionPrimary(info) 
			{
			    Function function = createASTNode(ASTNodes.FUNCTION);
			    function.setName(operator);
			    function.setArgs(new Expression[] {leftExpression, rightExpression});
			    leftExpression = function;
				rightExpression = null;
				operator = null;
			}
		)*
	)

	{
		return leftExpression;
	}
}

/*
name=star or slash
description=The * or / operator.
example={code:sql}/{code}
*/
String timesOperator() :
{
	Token operator = null;
}
{
	(	operator = <STAR> |
		operator = <SLASH>
	)

	{
		return operator.image;
	}
}

/*
name=value expression primary
description=A simple value expression.
example={code:sql}+col1{code}
*/
Expression valueExpressionPrimary(ParseInfo info) :
{
	String strVal = null;
	Expression ex = null;
}
{
	// Literal
	(ex=nonNumericLiteral()
	| [strVal = plusMinus()] 
	  (ex = unsignedNumericLiteral(strVal)
	   |
	   ex = unsignedValueExpressionPrimary(info) {
	     if (strVal != null && strVal.charAt(0) == '-') {
	        Function function = createASTNode(ASTNodes.FUNCTION);
	        function.setName("*"); //$NON-NLS-1$
	        function.setArgs(new Expression[] { constant(-1), ex });
	        ex = function;
	     }
	    }
	   )
	 )
	 {
		return ex;	 
	 }  
}

/*
name=unsigned value expression primary
description=An unsigned simple value expression.
example={code:sql}col1{code}
*/
Expression unsignedValueExpressionPrimary(ParseInfo info) :
{
	Expression expression = null;
	Token symbol = null;
	QueryCommand subquery = null;
	Integer index = null;
	Expression condition = null;
	Expression indexExpr = null;
}
{
        (
		// Reference
		<QMARK> {return reference(info.incrementReferenceCount());}
		|
		(<DOLLAR> index = intVal() {return reference(index -1); })
		|
		// Escaped function
		(	<ESCAPEDFUNCTION>
			expression=function(info)
			<RBRACE>
		)
		|		
		// Aggregate function
		LOOKAHEAD(2) ((expression=textAgg(info) | expression=aggregateSymbol(info) | expression=orderedAgg(info)) [condition = filterClause(info)] [expression = windowSpecification(expression, info)])
		|
		LOOKAHEAD(2) (expression=analyticAggregateSymbol(info) [condition = filterClause(info)] expression = windowSpecification(expression, info))
		|
		// Function
		LOOKAHEAD(2) (expression=function(info) [expression = windowSpecification(expression, info)])
		|
		// ElementSymbol
		((symbol=<ID> {
				String symbolImage = symbol.image;
				if(isStringLiteral(symbolImage, info)) {
				    return constant(normalizeStringLiteral(symbolImage));
				}  
			} | symbol=nonReserved()) 
			(<LSBRACE> indexExpr = commonValueExpression(info) <RSBRACE>)?
		)
		|
		LOOKAHEAD(subquery(info)) subquery = subquery(info)
		|
		( <LPAREN> 
 			expression = expression(info)
		  <RPAREN>
		  (<LSBRACE> indexExpr = commonValueExpression(info) <RSBRACE>)?
		)
		|
		// Searched CASE expressions
		LOOKAHEAD(2) expression = searchedCaseExpression(info)
		|
		// Non-searched CASE expressions
		expression = caseExpression(info)
	)						
		
	{
		if(symbol != null) {
			expression = elementSymbol(normalizeId(symbol.image));
		} else if (subquery != null){
			expression = scalarSubquery(subquery);
		}
		if (condition != null) {
			if (expression instanceof WindowFunction) {
    	   		((WindowFunction)expression).getFunction().setCondition(condition);
           	} else {
                ((AggregateSymbol)expression).setCondition(condition);
          	}
		}
		if (indexExpr != null) {
		    Function function = createASTNode(ASTNodes.FUNCTION);
            function.setName("array_get"); //$NON-NLS-1$
            function.setArgs(new Expression[] {expression, indexExpr});
            expression = function;
		}
		return expression;
	}
}

Reference reference(int referenceIndex) #Reference :
{
}
{
    {
        jjtThis.setIndex(referenceIndex);
        jjtThis.setPositional(true);
        return jjtThis;
    }
}

/*
name=window specification
description=The window specification for an analytical or windowed aggregate function.
example={code:sql}OVER (PARTION BY col1){code}
*/
Expression windowSpecification(Expression agg, ParseInfo info) :
{
	List<Expression> partitionList = null;
	OrderBy orderBy = null;
}
{
	<OVER> { 
	if (!(agg instanceof AggregateSymbol)) {
	   		throw new ParseException(Messages.getString(Messages.TeiidParser.invalid_window, agg));
    	}
    }
    <LPAREN> 
    [<PARTITION> <BY> partitionList = expressionList(info)]
    [orderBy = orderby(info)]
    <RPAREN>
    {
    	WindowFunction result = windowFunction();
    	WindowSpecification ws = newWindowSpecification();
    	result.setFunction((AggregateSymbol)agg);
    	ws.setPartition(partitionList);
    	ws.setOrderBy(orderBy);
    	result.setWindowSpecification(ws);
    	return result;
    }
}

/*
name=case expression
description=If/then/else chain using a common search predicand.
example={code:sql}CASE col1 WHEN 'a' THEN 1 ELSE 2{code}
*/
CaseExpression caseExpression(ParseInfo info) #CaseExpression :
{
	Expression expression = null, whenExpression = null, thenExpression = null, elseExpression = null;
	ArrayList whenExpressions = new ArrayList(2);
	ArrayList thenExpressions = new ArrayList(2);
}
{
	<CASE>
	expression = expression(info)
	(
		<WHEN>
		whenExpression = expression(info)
		<THEN>
		thenExpression = expression(info)
		{
			whenExpressions.add(whenExpression);
			thenExpressions.add(thenExpression);
		}
	)+
	[<ELSE> elseExpression = expression(info)]
	<END>
	{
	    jjtThis.setExpression(expression);
	    jjtThis.setWhen(whenExpressions, thenExpressions);
		jjtThis.setElseExpression(elseExpression);
		return jjtThis;
	}
}

/*
name=searched case expression
description=If/then/else chain using multiple search conditions.
example={code:sql}CASE WHEN x = 'a' THEN 1 WHEN y = 'b' THEN 2{code}
*/
SearchedCaseExpression searchedCaseExpression(ParseInfo info) #SearchedCaseExpression :
{
	Expression thenExpression = null, elseExpression = null;
	Criteria whenCriteria = null;
	List<Criteria> whenCriteriaList = new ArrayList<Criteria>(2);
    List<Expression> thenExpressions = new ArrayList<Expression>(2);
}
{
	<CASE>
	(
		<WHEN>
		whenCriteria = criteria(info)
		<THEN>
		thenExpression = expression(info)
		{
			whenCriteriaList.add(whenCriteria);
			thenExpressions.add(thenExpression);
		}
	)+
	
	[<ELSE> elseExpression = expression(info)]
	<END>
	{
	    jjtThis.setWhen(whenCriteriaList, thenExpressions);
		jjtThis.setElseExpression(elseExpression);
		return jjtThis;
	}
}

/*
name=function
description=Calls a scalar function.
example={code:sql}func('1', col1){code}
*/
Expression function(ParseInfo info) #Function :
{
	String funcName = null;
    
	Expression expression = null;
	ArrayList args = new ArrayList(2);
	ArrayList otherArgs = null;
	Token funcToken = null;
	Boolean distinct = null;
	OrderBy orderBy = null;
	Expression condition = null;
	String literal = null;
	ParsedDataType type = null;
}
{
	((	funcToken = <CONVERT>
		<LPAREN>
		expression = expression(info)
		{
			args.add(expression);
		}
		<COMMA>
		type = parseDataType()	
		{
			args.add(constant(type.getType()));
		}
		<RPAREN> 
	)
	|
	(			
		funcToken = <CAST>
		<LPAREN>
		expression = expression(info)
		{
			args.add(expression);
		} 
		<AS>
		type = parseDataType()
		{
			args.add(constant(type.getType()));
		}
		<RPAREN>				
	)
	|
	LOOKAHEAD(2) (
	   funcToken = <SUBSTRING>
	   <LPAREN>
	   expression = expression(info)
	   {
	      args.add(expression);
	   }
	   ((<FROM> expression = expression(info)
	   {
	      args.add(expression);
	   }
  	   [<FOR> expression = expression(info)
	    {
	      args.add(expression);
	    }
	   ])|(<COMMA> otherArgs = expressionList(info)) {args.addAll(otherArgs);})
	   <RPAREN>
	)
	|
	LOOKAHEAD(2) (
	   funcToken = <EXTRACT>
	   <LPAREN>
	   (funcToken = <YEAR> | 
	    funcToken = <MONTH> |
	    funcToken = <DAY> |
	    funcToken = <HOUR> |
	    funcToken = <MINUTE> |
	    funcToken = <SECOND>)
	   <FROM> expression = expression(info)
	   {
	   	  if (funcToken.image.equalsIgnoreCase("DAY")) {
	         funcName = "DAYOFMONTH";
	      }  	      
	      args.add(expression);
	   }
	   <RPAREN>
	)
	|
	LOOKAHEAD(2) (
	   funcToken = <TRIM>
	   <LPAREN>
	   { funcName = funcToken.image; funcToken = null; expression = null;}
	   [
	    LOOKAHEAD(2) (((funcToken = <LEADING>|
	      funcToken = <TRAILING>|
	      funcToken = <BOTH>)
	    [expression = expression(info)])
	    |
	    expression = expression(info))
		<FROM>
	   ]
  	   {
	      if (funcToken == null) {
	      	  args.add(constant("BOTH"));
	      } else {
	          args.add(constant(funcToken.image));
	      }
	      if (expression == null) {
	          args.add(constant(" "));
	      } else {
	          if (expression instanceof Constant) {
	              Object value = ((Constant)expression).getValue();
	              if (value instanceof String && ((String)value).length() != 1) {
				      throw new ParseException(Messages.getString(Messages.TeiidParser.char_val));
	              }
	          }
	          args.add(expression);
	      }
	   }
	   expression = expression(info)
	   {
	      args.add(expression);
	   }
	   <RPAREN>
	)
	|
	LOOKAHEAD(2) (	
		(funcToken = <TO_CHARS> | funcToken = <TO_BYTES>)
		<LPAREN>
		expression = expression(info) 
		{		
			args.add(expression);
		}
		<COMMA>
		literal = stringVal()
		{
			args.add(constant(literal));
		} 
		<RPAREN>				
	)
	|
	LOOKAHEAD(2) (	
		(funcToken = <TIMESTAMPADD> | funcToken = <TIMESTAMPDIFF>)
		<LPAREN>
		expression = intervalType() 
		{		
			args.add(expression);
		}
		<COMMA>
		expression = expression(info)
		{
			args.add(expression);
		} 
		<COMMA>
		expression = expression(info)
		{
			args.add(expression);
		} 
		<RPAREN>				
	)
	|
	LOOKAHEAD(2) expression = queryString(info)
	  {
		return expression;
	  }		
	|
	(
		(funcToken = <LEFT> | funcToken = <RIGHT> | funcToken = <CHAR> | funcToken = <USER> 
		                    | funcToken = <YEAR> | funcToken = <MONTH> | funcToken = <HOUR>
		                    | funcToken = <MINUTE> | funcToken = <SECOND> | funcToken = <XMLCONCAT>
		                    | funcToken = <XMLCOMMENT>)
        <LPAREN>
		[args = expressionList(info)]
		<RPAREN>
	)		
	|
	LOOKAHEAD(<TRANSLATE>|<INSERT> <LPAREN>) (
		(funcToken = <TRANSLATE> | funcToken = <INSERT>)
		<LPAREN>
		[args = expressionList(info)]
		<RPAREN>
	)  
	| expression = xmlParse(info)
	  {
	  	return expression;
	  }
	| expression = xmlElement(info)
	  {
	  	return expression;
	  }
	| ( funcToken = <XMLPI>
		<LPAREN>
		(
		 [LOOKAHEAD(1) <NAME>] literal = id(null)
		)
		{
			args.add(constant(literal));	
		}
		[
			<COMMA> expression = expression(info)
			{
				args.add(expression);	
			}	
		]
		<RPAREN>
	)
	|	expression = xmlForest(info)
		{
			return expression;
		}
	|	LOOKAHEAD(<JSONOBJECT>) expression = jsonObject(info)
		{
			return expression;
		}
	|	expression = xmlSerialize(info)
		{
			return expression;
		}
	|	expression = xmlQuery(info)
		{
			return expression;
		}
	|   
	(	funcName = id(null)
		<LPAREN>
		[<ALL> { distinct = false; } | <DISTINCT> {distinct = true;} ]
		[ args = expressionList(info) ]
		[ orderBy = orderby(info) ]
		<RPAREN>
		[condition = filterClause(info)]
	))		
	{
		if(funcName == null) { 
			funcName = funcToken.image;
		}
		if (distinct != null || orderBy != null || condition != null || "string_agg".equalsIgnoreCase(funcName)) {
		    AggregateSymbol as = newAggregateSymbol(funcName, distinct!=null?distinct:false, (Expression[])args.toArray(new Expression[args.size()]));
		    as.setOrderBy(orderBy);
			as.setCondition(condition);
			return as;
        }
        jjtThis.setName(funcName);
        jjtThis.setArgs((Expression[])args.toArray(new Expression[args.size()]));
        return jjtThis;
	}
}

/*
name=xml parse
description=Parses the given value as XML.
example={code:sql}XMLPARSE(DOCUMENT doc WELLFORMED){code}
*/
XMLParse xmlParse(ParseInfo info) #XMLParse :
{
	Expression expr = null;
	boolean doc = false;
	boolean wellformed = false;
}
{
	<XMLPARSE><LPAREN>
	(<DOCUMENT> {doc = true;}| <CONTENT> {doc = false;})
	expr = expression(info)
	[<WELLFORMED>
	 {
	   wellformed = true;
	 }
	]
	<RPAREN>
	{
		jjtThis.setDocument(doc);
		jjtThis.setExpression(expr);
		jjtThis.setWellFormed(wellformed);
		return jjtThis;
	}
}

/*
name=querystring function
description=Produces a URL query string from the given arguments.
example={code:sql}QUERYSTRING(col1 AS opt, col2 AS val){code}
*/
QueryString queryString(ParseInfo info) #QueryString :
{
	Expression path = null;
	DerivedColumn arg = null;
	ArrayList<DerivedColumn> args = new ArrayList<DerivedColumn>(2);
}
{
 	<QUERYSTRING> <LPAREN>
 	path = expression(info)
	(<COMMA> arg=derivedColumn(info)
	 {
		args.add(arg);
		arg = null;
	 }
	)*
	<RPAREN>  		
	{
	    jjtThis.setPath(path);
	    jjtThis.setArgs(args);
		return jjtThis;
	}
}

/*
name=xml element
description=Creates an XML element.
example={code:sql}XMLELEMENT(NAME "root", child){code}
*/
XMLElement xmlElement(ParseInfo info) #XMLElement :
{
	String name = null;
	ArrayList content = new ArrayList(2);
	XMLNamespaces xmlNamespaces = null;
	XMLAttributes xmlAttributes = null;
	Expression expression = null;
}
{
	<XMLELEMENT> <LPAREN>
	(
	 [LOOKAHEAD(1) <NAME>] name = id(null)
	)
	[
		LOOKAHEAD(2) <COMMA> xmlNamespaces = xmlNamespaces(info)
	]
	[
		LOOKAHEAD(2) <COMMA> xmlAttributes = xmlAttributes(info)
	]
	(<COMMA> expression=expression(info)
	{
		content.add(expression);
	}
	)*
	<RPAREN>
	{
	    jjtThis.setName(name);
	    jjtThis.setContent(content);
		jjtThis.setNamespaces(xmlNamespaces);
		jjtThis.setAttributes(xmlAttributes);
		return jjtThis;
	}
}

/*
name=xml attributes
description=Creates attributes for the containing element.
example={code:sql}XMLATTRIBUTES(col1 AS attr1, col2 AS attr2){code}
*/
XMLAttributes xmlAttributes(ParseInfo info) #XMLAttributes :
{
	DerivedColumn expression = null;
	ArrayList<DerivedColumn> args = new ArrayList<DerivedColumn>(2);
}
{
 	<XMLATTRIBUTES> <LPAREN>
	expression = derivedColumn(info)
	{
		args.add(expression);
	}
	(<COMMA> expression=derivedColumn(info)
	 {
		args.add(expression);
	 }
	)*
	<RPAREN>  		
	{
	   jjtThis.setArgs(args);
	   return jjtThis;
	}
}

/*
name=json object
description=Produces a JSON object containing name value pairs.
example={code:sql}JSONOBJECT(col1 AS val1, col2 AS val2){code}
*/
JSONObject jsonObject(ParseInfo info) #JSONObject :
{
	ArrayList<DerivedColumn> args = null;
}
{
 	<JSONOBJECT> <LPAREN>
 	args = derivedColumnList(info)
 	<RPAREN>  		
	{
	    jjtThis.setArgs(args);
		return jjtThis;
	}
}

/*
name=derived column list
description=a list of name value pairs
example={code:sql}col1 AS val1, col2 AS val2{code}
*/
ArrayList<DerivedColumn> derivedColumnList(ParseInfo info) :
{
	DerivedColumn expression = null;
	ArrayList<DerivedColumn> args = new ArrayList<DerivedColumn>(2);
}
{
	expression = derivedColumn(info)
	{
		args.add(expression);
	}
	(<COMMA> expression=derivedColumn(info)
	 {
		args.add(expression);
	 }
	)*
	{ 
		return args;
	}
}

/*
name=xml forest
description=Produces an element for each derived column.
example={code:sql}XMLFOREST(col1 AS ELEM1, col2 AS ELEM2){code}
*/
XMLForest xmlForest(ParseInfo info) #XMLForest :
{
	DerivedColumn expression = null;
	XMLNamespaces xmlNamespaces = null;
	ArrayList<DerivedColumn> args = null;
}
{
 	<XMLFOREST> <LPAREN>
	[
		xmlNamespaces = xmlNamespaces(info) <COMMA>
	]
	args = derivedColumnList(info)
	<RPAREN>  		
	{
		jjtThis.setArguments(args);
		jjtThis.setNamespaces(xmlNamespaces);
		return jjtThis;
	}
}

/*
name=xml namespaces
description=Defines XML namespace URI/prefix combinations
example={code:sql}XMLNAMESPACES('http://foo' AS foo){code}
*/
XMLNamespaces xmlNamespaces(ParseInfo info) #XMLNamespaces :
{
	List<NamespaceItem> namespaces = new ArrayList<NamespaceItem >(2);
	NamespaceItem item = null;
}
{
 	<XMLNAMESPACES> <LPAREN>
	item = namespaceItem()
	{
		namespaces.add(item);
	}
	(<COMMA> item = namespaceItem()
	 {
		 namespaces.add(item);
	 }
	)*
	<RPAREN>
	{
	    jjtThis.setNamespaces(namespaces);
	    return jjtThis;
	}
}

/*
name=xml namespace element
description=An xml namespace
example={code:sql}NO DEFAULT{code}
*/
NamespaceItem namespaceItem() :
{
	String uri = null;
	String prefix = null;
}
{
	(
		uri = stringVal() <AS> prefix = id(null)
		{
			return new NamespaceItem(uri, prefix);
		}
	) | (
		<NO> <DEFAULT_KEYWORD>
		{
			return new NamespaceItem();
		}
	) | (
		<DEFAULT_KEYWORD> uri = stringVal()
		{
			return new NamespaceItem(uri);
		}
	)
}

/*
name=data type
description=A data type.
example={code:sql}STRING{code}
*/
ParsedDataType parseDataType() :
{
	Token typeToken = null;
	Integer length = null;
	Integer scale = null; 
	boolean precision = true;
}
{
	(
		(typeToken = <STRING> ([<LPAREN>length=intVal()<RPAREN>]) {precision = false;} ) |
		(typeToken = <VARCHAR> ([<LPAREN>length=intVal()<RPAREN>]) {precision = false;} ) | 
		typeToken = <BOOLEAN> | 
		typeToken = <BYTE> | 
		typeToken = <TINYINT> |
		typeToken = <SHORT> | 
		typeToken = <SMALLINT> |
		(typeToken = <CHAR> ([<LPAREN>length=intVal()<RPAREN>]) {precision = false;} ) | 
		typeToken = <INTEGER> | 
		typeToken = <LONG> | 
		typeToken = <BIGINT> |
		(typeToken = <BIGINTEGER> [<LPAREN>length=intVal()<RPAREN>]) | 
		typeToken = <FLOAT> |
		typeToken = <REAL> | 
		typeToken = <DOUBLE> | 
		(typeToken = <BIGDECIMAL> [<LPAREN>length=intVal()[<COMMA>scale=intVal()]<RPAREN>])|
		(typeToken = <DECIMAL> [<LPAREN>length=intVal()[<COMMA>scale=intVal()]<RPAREN>])| 
		typeToken = <DATE> | 
		typeToken = <TIME> |
		typeToken = <TIMESTAMP> |
		(typeToken = <OBJECT> ([<LPAREN>length=intVal()<RPAREN>]) {precision = false;} )|
		(typeToken = <BLOB> ([<LPAREN>length=intVal()<RPAREN>]) {precision = false;} )|
		(typeToken = <CLOB> ([<LPAREN>length=intVal()<RPAREN>]) {precision = false;} )|
		(typeToken = <VARBINARY> ([<LPAREN>length=intVal()<RPAREN>]) {precision = false;} ) |
		typeToken = <XML>
		
	)
	
	{
		if (scale != null){
			return new ParsedDataType(typeToken.image, length, scale, precision);
		}
		else if (length != null){
			return new ParsedDataType(typeToken.image, length, precision);
		}
		return new ParsedDataType(typeToken.image);
	}
}

/*
name=time interval
description=A time interval keyword.
example={code:sql}SQL_TSI_HOUR{code}
*/
Constant intervalType() : 
{
	Token type = null;
}
{
	(
		type = <SQL_TSI_FRAC_SECOND>
	   |type = <SQL_TSI_SECOND>
	   |type = <SQL_TSI_MINUTE>
	   |type = <SQL_TSI_HOUR>
	   |type = <SQL_TSI_DAY>
	   |type = <SQL_TSI_WEEK>
	   |type = <SQL_TSI_MONTH>
	   |type = <SQL_TSI_QUARTER>
	   |type = <SQL_TSI_YEAR>
	)
	{
		return constant(type.image);
	}
}

/*
name=non numeric literal
description=An escaped or simple non numeric literal.
example={code:sql}'a'{code}
*/
Constant nonNumericLiteral() :
{
    Token t = null;
    String strVal = "";
    DataTypeManagerService.DefaultDataTypes escapeType = null;
    Constant constant = null;
}
{
	(	
		strVal=stringVal()	{ 
			constant = constant(strVal, DataTypeManagerService.DefaultDataTypes.STRING);
		} |
		t=<BINARYSTRINGVAL> { constant = constant(new BinaryType(javax.xml.bind.DatatypeConverter.parseHexBinary(t.image.substring(2, t.image.length() - 1))), DataTypeManagerService.DefaultDataTypes.VARBINARY); } |
		t=<FALSE>    { constant = constant(Boolean.FALSE, DataTypeManagerService.DefaultDataTypes.BOOLEAN); } |
		t=<TRUE>     { constant = constant(Boolean.TRUE, DataTypeManagerService.DefaultDataTypes.BOOLEAN); }  |
        t=<UNKNOWN>  { constant = constant(null, DataTypeManagerService.DefaultDataTypes.BOOLEAN); }       |
        t=<NULL>     { constant = constant(null); }          |
        
        ( <ESCAPEDTYPE> { 
            String image = getToken(0).image; 
        	if (StringUtil.endsWithIgnoreCase(image, "b")) {
        		escapeType=DataTypeManagerService.DefaultDataTypes.BOOLEAN;
        	} else if (StringUtil.endsWithIgnoreCase(image, "ts")) {
        		escapeType=DataTypeManagerService.DefaultDataTypes.TIMESTAMP;
        	} else if (StringUtil.endsWithIgnoreCase(image, "d")) {
        		escapeType=DataTypeManagerService.DefaultDataTypes.DATE;
        	} else {
        		escapeType=DataTypeManagerService.DefaultDataTypes.TIME;
        	}
		  } strVal=stringVal() { 
	        	try {
	        		constant = constant(getDataTypeService().transformValue(strVal, escapeType), escapeType);
	        	} catch (Exception e) {
	        		throw new ParseException(e.getMessage());
	        	}
	        } <RBRACE> )

	)
	{
		return constant;
	}
}

/*
name=unsigned numeric literal
description=An unsigned numeric literal value.
example={code:sql}1.234{code}
*/
Constant unsignedNumericLiteral(String strVal) :
{
    Token t = null;
}
{
		(t=<UNSIGNEDINTEGER> 	{ 
			try { 
			    strVal = prependSign(strVal, t.image);
				// First try as integer - this should nearly always work
				return constant(Integer.valueOf(strVal), DataTypeManagerService.DefaultDataTypes.INTEGER); 				
			} catch(NumberFormatException e1) { 
				try { 
					// Second try as a long
					return constant(Long.valueOf(strVal), DataTypeManagerService.DefaultDataTypes.LONG);
				} catch(NumberFormatException e2) {
					try {
						// Third try as a biginteger
						return constant(new BigInteger(strVal), DataTypeManagerService.DefaultDataTypes.BIG_INTEGER);
					} catch(NumberFormatException e3) {
						Object[] params = new Object[] { strVal };
						throw new ParseException(Messages.getString(Messages.TeiidParser.Integer_parse, params));
					}
				}
			}			
		}	| t=<FLOATVAL>	{ 
		    strVal = prependSign(strVal, t.image);
			try { 
				return constant(Double.valueOf(strVal), DataTypeManagerService.DefaultDataTypes.DOUBLE); 
			} catch(NumberFormatException e) {
				Object[] params = new Object[] { strVal };
				throw new ParseException(Messages.getString(Messages.TeiidParser.Float_parse, params));
			}
		}  | t=<DECIMALVAL>	{ 
     		strVal = prependSign(strVal, t.image);
			try { 
			    if (getDataTypeService().isDecimalAsDouble()) {
			    	return constant(Double.valueOf(strVal), DataTypeManagerService.DefaultDataTypes.DOUBLE);
				} else {
			    	return constant(new java.math.BigDecimal(strVal), DataTypeManagerService.DefaultDataTypes.BIG_DECIMAL); 
				}
			} catch(NumberFormatException e) {
				Object[] params = new Object[] { t.image };
				throw new ParseException(Messages.getString(Messages.TeiidParser.decimal_parse, params));
			}
		} )
}

/*
unused=true
*/
 void parseMetadata(MetadataFactory factory) :
{
}
{
    (ddlStmt(factory))*
    <EOF>     
}

/*
name=ddl statement
description=A data definition statement.
example={code:sql}CREATE FOREIGN TABLE X (Y STRING){code}
index=true
*/
void ddlStmt(MetadataFactory factory) :
{
	Command command = null; 
}
{
	(LOOKAHEAD(3) 	createTable(factory) | 
	 LOOKAHEAD(4)	createDDLProcedure(factory, ParseInfo.DEFAULT_INSTANCE) |
	 LOOKAHEAD(2)	createNameSpace(factory) |
	 LOOKAHEAD(4)	alterStatement(factory) |
	 LOOKAHEAD(4) 	command=createTrigger(ParseInfo.DEFAULT_INSTANCE)
	 {
	 	createDDLTrigger(factory, (AlterTrigger)command);
	 }
	)
	[<SEMICOLON>]
}

/*
name=option namespace
description=A namespace used to shorten the full name of an option key.
example={code:sql}SET NAMESPACE 'http://foo' AS foo{code}
*/
void createNameSpace(MetadataFactory factory) :
{
	String uri = null;
	String prefix = null;
}
{ 
	<SET> <NAMESPACE> uri = stringVal() <AS> prefix = id(Boolean.FALSE)
	{
		factory.addNamespace(prefix, uri);
	}
}

/*
name=create procedure
description=Defines a procedure or function invocation.
example={code:sql}CREATE FOREIGN PROCEDURE proc (param STRING) RETURNS STRING{code}
*/
CreateProcedureCommand createDDLProcedure(MetadataFactory factory, ParseInfo info) :
{
	boolean virtual = true;
	boolean function = false;
	Procedure proc = null;
	String procName = null;
    CreateProcedureCommand procCmd = createASTNode(ASTNodes.CREATE_PROCEDURE_COMMAND);
    Statement stmt = null; 	
    ParsedDataType returnDataType = null;
    Token comment = null;    
}
{
	<CREATE> [<VIRTUAL> | <FOREIGN> {virtual = false;}] (<PROCEDURE> | <FUNCTION> {function = true;})
	//the below is optional beacuse to allow the designer based metadata
	(	procName = id(null)
		{
			proc = factory.addProcedure(procName);
		}
	
		<LPAREN>
		[procedureParameter(factory, proc)
		(<COMMA> procedureParameter(factory, proc))*]	
		<RPAREN>
		
		[<RETURNS> (([<TABLE>] <LPAREN>
	        procedureRsColumn(factory, proc)
	        (<COMMA> procedureRsColumn(factory, proc))*
	    <RPAREN>)
	     |
	     returnDataType = parseDataType())
	    ]
	    [ optionsClause(proc, factory)
		   {
		      setProcedureOptions(proc);
		   }
		]
		[<AS> { comment = getToken(1).specialToken; } 
		stmt = statement(info)])
		{
			proc.setVirtual(virtual);
			proc.setFunction(function);
			if (stmt != null){
			    if (function || !virtual) {
			         throw new ParseException(Messages.getString(Messages.TeiidParser.function_def, procName));
			    }
				proc.setQueryPlan((comment != null?comment.image+" ":"") + stmt.toString());
			}
			
	        if (returnDataType != null){
				addProcColumn(factory, proc, "return", returnDataType, false);
	        }
	        if (function) {
	        	replaceProcedureWithFunction(factory, proc);
	        }
	        return procCmd; 		
		}
}

/*
name=procedure parameter
description=A procedure or function parameter
example={code:sql}OUT x INTEGER{code}
*/
void procedureParameter(MetadataFactory factory, Procedure proc) :
{
	String name = null;
	ParsedDataType type = null;
	ProcedureParameter.Type ppType = ProcedureParameter.Type.In;
	ProcedureParameter param = null;
	String defaultValue = null;
	boolean notNull = false;
	boolean vararg = false;
}
{
	[LOOKAHEAD(1) ((<IN> {ppType = ProcedureParameter.Type.In ;})
	 |(<OUT> {ppType = ProcedureParameter.Type.Out;})
	 |(<INOUT> {ppType = ProcedureParameter.Type.InOut;})
	 |(<VARIADIC> {ppType = ProcedureParameter.Type.In ; vararg = true;}))]
	name = id(Boolean.TRUE)
	type = parseDataType()
	[<NOT> <NULL> {notNull = true;}]
	[<RESULT>
	  {
	    if (ppType != ProcedureParameter.Type.Out) {
	    	throw new ParseException(Messages.getString(Messages.TeiidParser.param_out, proc.getName(), name));
	    }
	    ppType = ProcedureParameter.Type.ReturnValue;
	  }
	]
	{
		param = factory.addProcedureParameter(name, type.getType(), ppType, proc);
		setTypeInfo(type, param);
		if (notNull) {
			param.setNullType(Column.NullType.No_Nulls);
		}
		if (vararg) {
			param.setVarArg(vararg);
		}			
	}
	[<DEFAULT_KEYWORD> defaultValue = stringVal() {param.setDefaultValue(defaultValue);}]
	[optionsClause(param, factory)
	   {
	      setColumnOptions(param);
	   }
	]	
}

/*
name=procedure result column
description=A procedure result column.
example={code:sql}x INTEGER{code}
*/
void procedureRsColumn(MetadataFactory factory, Procedure proc) :
{
	String name = null;
	ParsedDataType type = null;
	boolean notNull = false;
	BaseColumn column = null; 
}
{
      name = id(Boolean.TRUE)
	  type = parseDataType()
	  {
	  	column = addProcColumn(factory, proc, name, type, true);
	  }
	  [<NOT> <NULL> {column.setNullType(Column.NullType.No_Nulls);}]	  
	  [optionsClause(column, factory)
	   {
	      setColumnOptions(column);
	   }
	  ]
}

/*
name=create table
description=Defines a table or view.
example={code:sql}CREATE VIEW vw AS SELECT 1{code}
*/
void createTable(MetadataFactory factory) :
{
	boolean view = false;
	String tableName = null;
	Table table = null;
	Token comment = null;
	Command query = null;
}
{
	<CREATE> (<FOREIGN> <TABLE> | [<VIRTUAL>] <VIEW> {view = true;}) 
	tableName = id(null)
	{
	    table = factory.addTable(tableName);
	    table.setVirtual(view);
	}
	(createTableBody(table, factory) | [optionsClause(table, factory)
	   {
	      setTableOptions(table);
	   }
	])
	[<AS> { comment = getToken(1).specialToken; } query = queryExpression(ParseInfo.DEFAULT_INSTANCE)
	  {
		   if (!view) {
     		   throw new ParseException(Messages.getString(Messages.TeiidParser.view_def, table.getName()));
		   }
		   table.setSelectTransformation((comment != null?comment.image+" ":"") + query.toString());	
	  }
	]
}

/*
name=create foreign temp table
description=Defines a foreign temp table
example={code:sql}CREATE FOREIGN TEMPORARY TABLE t (x string) ON z{code}
*/
Create createForeignTempTable(ParseInfo info) :
{
	Table table = null;
	String tableName = null;
	String model = null;
	MetadataFactory factory = getTempMetadataFactory();
}
{
	<CREATE> <FOREIGN> <TEMPORARY> <TABLE>
	tableName = id(null)
	{
		table = new Table();
		//need to set a dummy/stable uuid
		table.setUUID("tid:0");
		table.setTableType(Table.Type.TemporaryTable);
		table.setName(tableName);
	} 
	createTableBody(table, factory)
	<ON> model = id(null)
	{
		Create create = createASTNode(ASTNodes.CREATE);
		create.setTableMetadata(table);
		create.setOn(model);
		return create;
	}
}

/*
name=create table body
description=Defines a table.
example={code:sql}(x string) OPTIONS (CARDINALITY 100){code}
*/
void createTableBody(Table table, MetadataFactory factory) :
{
	String name = null;
	AbstractMetadataRecord constraint = null;
}
{
	<LPAREN>
    createColumn(factory, table)
    (LOOKAHEAD(3) <COMMA>
      createColumn(factory, table)
    )*	
    (<COMMA>{name=null;} [<CONSTRAINT> name = id(null)] (constraint = primaryKey(factory, table, name) | constraint = constraint(factory, table, name, ParseInfo.DEFAULT_INSTANCE) | constraint = foreignKey(factory, table, name))
       [optionsClause(constraint, factory)
	   {
	      setCommonProperties(constraint, constraint.getProperties());
	   }
	]
    )*
    <RPAREN>    
    [optionsClause(table, factory)
	   {
	      setTableOptions(table);
	   }
	]
	{
		if (table.getColumns() != null && !table.supportsUpdate()) {
			for (Column c : table.getColumns()) {
				c.setUpdatable(false);
			}
		}
	}
}

/*
name=foreign key
description=Defines the foreign key referential constraint.
example={code:sql}FOREIGN KEY (a, b) REFERENCES tbl (x, y){code}
*/
AbstractMetadataRecord foreignKey(MetadataFactory factory, Table table, String name) :
{
	List<String> columnNames = null;
	String viewName = null;
	Table reference = null;
	KeyRecord pk = null;
	Column column = null;
	List<String> pkColumnNames = null;
	KeyRecord key = null;
}
{
	<FOREIGN> <KEY>
	columnNames = columnList(true, false)
    <REFERENCES>
    viewName = id(null)
    [pkColumnNames = columnList(true, false)]
    {
	    return factory.addForiegnKey(name != null?name:("FK"+table.getForeignKeys().size()), columnNames, pkColumnNames, viewName, table);   
    }
}

/*
name=primary key
description=Defines the primary key.
example={code:sql}PRIMARY KEY (a, b){code}
*/
AbstractMetadataRecord primaryKey(MetadataFactory factory, Table table, String name) :
{
	List<String> columnNames = null;
	Column column = null;
	KeyRecord key = null;
}
{
	<PRIMARY> <KEY>
	columnNames = columnList(true, false)
    {
	    if (table.getPrimaryKey() != null){
	    	throw new ParseException(Messages.getString(Messages.TeiidParser.pk_exists, table.getName()));
	 	}
 		return factory.addPrimaryKey(name!=null?name:"PK", columnNames, table);
    }
}

/*
name=other constraints
description=Defines ACCESSPATTERN and UNIQUE constraints and INDEXes. 
example={code:sql}UNIQUE (a){code}
*/
AbstractMetadataRecord constraint(MetadataFactory factory, Table table, String name, ParseInfo info) :
{
	KeyRecord keyrecord = null;
	Column column = null;
	List<String> columnNames = null;
	Token type = null;
	KeyRecord key = null;
	List<Expression> expressions = null;
}
{
	(( type = <UNIQUE> | type = <ACCESSPATTERN>)
	columnNames = columnList(true, false)	    
	{
		if (type.image.equalsIgnoreCase("UNIQUE")) { 
    		return factory.addIndex(name != null?name:("UNIQUE"+table.getUniqueKeys().size()), false, columnNames, table);
    	} else if (type.image.equalsIgnoreCase("ACCESSPATTERN")) { 
    		return factory.addAccessPattern(name != null?name:("AP"+table.getAccessPatterns().size()), columnNames, table);
    	}
    }) | (
    type = <INDEX>
    <LPAREN>
 	expressions = expressionList(info)
 	<RPAREN>
 	 {
 		return addFBI(factory, expressions, table, name);
 	 }
 	)
}

/*
name=column list
description=A list of column names.
example={code:sql}(a, b){code}
*/
List columnList(boolean validate, boolean asElementSymbols) :
{
	ArrayList list = new ArrayList();
	String id = null;
}
{
	<LPAREN>
	  id = id(validate?Boolean.TRUE:null)
	  {
		if (asElementSymbols) {
			list.add(elementSymbol(id));
		} else {
	  		list.add(id);
	  	}
	  }
	  (<COMMA> id = id(validate?Boolean.TRUE:null)
	   {
		if (asElementSymbols) {
			list.add(elementSymbol(id));
		} else {
	  		list.add(id);
	  	}
	   }
	  )*
	 <RPAREN> 
	 {
	 	return list;
	 }
}

/*
name=table element
description=Defines a table column.
example={code:sql}x INTEGER NOT NULL{code}
*/
void createColumn(MetadataFactory factory, Table table) :
{
	String element = null;
	ParsedDataType type = null;
	boolean autoIncrement = false;
	Boolean notNull = false;
	String defalt = null;
	Column column = null;
	List<String> columnName = new ArrayList<String>();
	boolean index = false;
	boolean unique = false;
	boolean pk = false;
	Token word = null;
}
{
	 element = id(Boolean.TRUE)
	 (
	  type = parseDataType()
	  {
		column = factory.addColumn(element, type.getType(), table);
		column.setUpdatable(true);
	    setTypeInfo(type, column);		    	
		columnName.add(element);
	  }
	 )
	 [<NOT> <NULL> { column.setNullType(Column.NullType.No_Nulls); }]
	 [<AUTO_INCREMENT> {  autoIncrement = true; }]
	 ((<PRIMARY> <KEY> { pk = true; }) | ([<UNIQUE> { unique = true;  }] [<INDEX> { index = true; }]))
	 [<DEFAULT_KEYWORD> defalt = stringVal() {column.setDefaultValue(defalt);}]
	 [optionsClause(column, factory)
	   {
	      setColumnOptions(column);
	   }
	 ]
	 {
	    if (index){
	 		factory.addIndex("INDEX"+table.getIndexes().size(), true, columnName, table);
	 	} else if (unique){
	 		factory.addIndex("UNIQUE"+table.getIndexes().size(), false, columnName, table);
	 	} else if (pk) {
	    	if (table.getPrimaryKey() != null) {
	    		throw new ParseException(Messages.getString(Messages.TeiidParser.pk_exists, table.getName()));
	    	}
	 		factory.addPrimaryKey("PK", columnName, table);
	 	}
	     	 	
	    column.setAutoIncremented(autoIncrement);
	 }
}

/*
name=options clause
description=A list of statement options.
example={code:sql}OPTIONS ('x' 'y', 'a' 'b'){code}
*/
void optionsClause(AbstractMetadataRecord record, MetadataFactory factory) :
{
}
{
  <OPTIONS> <LPAREN>
  optionPair(record, factory) (<COMMA> optionPair(record, factory))*	   
  <RPAREN> 
}

/*
name=option pair
description=An option key/value pair.
example={code:sql}'key' 'value'{code}
*/
void optionPair(AbstractMetadataRecord record, MetadataFactory factory) :
{
	Constant value = null;
	String key = null;
	Token t = null;
	String strVal = null;
}
{
	 key = id(null)
	 (value = nonNumericLiteral()
	 | [strVal = plusMinus()] value = unsignedNumericLiteral(strVal))
	 { 
	 	key = resolvePropertyKey(factory, key);
	 	String val = null;
	 	if (value.getValue() != null) {
	 		val = value.getValue().toString();
	 	}
	 	record.setProperty(key, val); 
	 } 
}

/*
name=alter options
description=alters options of tables/procedure
example={code:sql}ALTER FOREIGN TABLE foo OPTIONS (ADD cardinality 100){code}
*/
void alterStatement(MetadataFactory factory) :
{
	boolean table = false;
	boolean proc = false;
	boolean func = false;
	String objectName = null;
	AbstractMetadataRecord record = null;
}
{ 
	<ALTER> [<VIRTUAL> | <FOREIGN>] (<TABLE> {table = true;} | <VIEW> {table = true;} | <PROCEDURE> {proc=true;})
	objectName = id(null)
	{
		if (table){
			record = factory.getSchema().getTable(objectName);
			if (record == null){
				throw new ParseException(Messages.getString(Messages.TeiidParser.alter_table_doesnot_exist, objectName));
			}
		}
		if (proc){
			record = factory.getSchema().getProcedure(objectName);
			if (record == null){
				throw new ParseException(Messages.getString(Messages.TeiidParser.alter_procedure_doesnot_exist, objectName));
			}
		}		
	}
	
	(alterOptionsList(record, factory) | 
	 alterColumn(record, factory))
}

/*
name=alter options list
description=a list of alterations to options
example={code:sql}OPTIONS (ADD updatable true){code}
*/
void alterOptionsList(AbstractMetadataRecord record, MetadataFactory factory) :
{

}
{
  <OPTIONS> <LPAREN>
  (addSetOption(record, factory) |
   dropOption(record))
  (<COMMA> (addSetOption(record, factory) |
   dropOption(record)))*
  <RPAREN>
  {
 		setOptions(record);
  }
}

/*
name=drop option
description=drop option
example={code:sql}DROP updatable{code}
*/
void dropOption(AbstractMetadataRecord record) :
{
	String key = null;
}
{
   <DROP> key = id(null) 
   {
 		removeOption(key, record);
   }
}

/*
name=add set option
description=add or set an option pair
example={code:sql}ADD updatable true{code}
*/
void addSetOption(AbstractMetadataRecord record, MetadataFactory factory) :
{
}
{
   (<ADD>|<SET>) optionPair(record, factory) 
}

/*
name=alter column options
description=alters a set of column options
example={code:sql}ALTER COLUMN bar OPTIONS (ADD updatable true){code}
*/
void alterColumn(AbstractMetadataRecord parent, MetadataFactory factory) :
{
	boolean param = false;
	String objectName = null;
	AbstractMetadataRecord record = null;
	String key = null;
}
{ 
	<ALTER> [<COLUMN>|<PARAMETER>{param=true;}]
	objectName = id(null)
	{
		record = getChild(objectName, parent, param);
	}
	alterOptionsList(record, factory)
}

AssignmentStatement assignmentStatement(ElementSymbol elementSymbol, LanguageObject value) #AssignmentStatement :
{
}
{
    {
        jjtThis.setVariable(elementSymbol);

        if (value instanceof Expression)
            jjtThis.setExpression((Expression) value);
        else if (value instanceof QueryCommand)
            jjtThis.setExpression(scalarSubquery((QueryCommand) value));
        else
            jjtThis.setCommand((Command) value);

        return jjtThis;
    }
}

ScalarSubquery scalarSubquery(QueryCommand queryCommand) #ScalarSubquery :
{
}
{
    {
        jjtThis.setCommand(queryCommand);
        return jjtThis;
    }
}

GroupSymbol groupSymbol(String... args) #GroupSymbol :
{
}
{ 
    {
        /*
         * Expectation is that group symbol args should be
         * name then definition (or alias).
         */
        jjtThis.setName(args[0]);
        if (args.length == 2)
            jjtThis.setDefinition(args[1]);

        return jjtThis;
    }
}

Constant constant(Object... args) #Constant :
{
}
{ 
    {
        if (args == null)
            return jjtThis;

        /*
         * Expectation is that constant args should be
         * value of type Object and type of type Class.
         */
        for (Object arg : args) {
            if (arg instanceof Class) {
                jjtThis.setType((Class) arg);
            } else if (arg instanceof DataTypeManagerService.DefaultDataTypes) {
                DataTypeManagerService.DefaultDataTypes dataType = (DataTypeManagerService.DefaultDataTypes) arg;
                jjtThis.setType(dataType.getTypeClass());
            } else {
                jjtThis.setValue(arg);
            }
        }
        return jjtThis;
    }
}

ElementSymbol elementSymbol(String name) #ElementSymbol :
{
}
{ 
    {
        jjtThis.setName(name);
        return jjtThis;
    }
}

Block block() #Block :
{
}
{
    {
        return jjtThis;
    }
}

Block asBlock(Statement stmt) :
{
  Block b = null;
}
{
    {
        if (stmt == null)
            return null;

        if (stmt instanceof Block)
            return (Block) stmt;

        b = block();
        b.addStatement(stmt);
        return b;
    }
}

ExpressionCriteria expressionCriteria(Expression expression) #ExpressionCriteria :
{
}
{
    {
        jjtThis.setExpression(expression);
        return jjtThis;
    }
}

AliasSymbol aliasSymbol(String alias, Expression expression) #AliasSymbol :
{
}
{
    {
        jjtThis.setName(alias);
        jjtThis.setSymbol(expression);
        return jjtThis;
    }
}

AggregateSymbol newAggregateSymbol(String name, boolean isDistinct, Expression[] arguments) #AggregateSymbol :
{
}
{
    {
        jjtThis.setName(name);
        jjtThis.setDistinct(isDistinct);
        jjtThis.setArgs(arguments);
        return jjtThis;
    }
}

WindowFunction windowFunction() #WindowFunction :
{
}
{
    {
        return jjtThis;
    }
}

WindowSpecification newWindowSpecification() #WindowSpecification :
{
}
{
    {
        return jjtThis;
    }
}

TextLine textLine() #TextLine :
{
}
{
    {
        return jjtThis;
    }
}

AlterTrigger alterTrigger() #AlterTrigger :
{
}
{
    {
        return jjtThis;
    }
}

AlterProcedure alterProcedure() #AlterProcedure :
{
}
{
    {
        return jjtThis;
    }
}

AlterView alterView() #AlterView :
{
}
{
    {
        return jjtThis;
    }
}

Array array() #Array :
{
}
{
    {
        return jjtThis;
    }
}